// ===============================
// poseLib
// ===============================
//
// Purpose:
// --------
// Allows you to record poses for anything in Maya (most likely the controls of a character rig).
// based on poseLib script from  Lionel Gallat
//
//
//
//
// Usage:
// ------
//      poseLib;
//

global string $poseLibVersion; // Declared in the Python module.
global string $poseLibModule;
global string $poseLibPublicStatus;
global string $poseLibDefaultArchetypes[];
global string $poseLibDefaultCasting[];
global string $poseLibDefaultCharacters[];
global string $poseLibDefaultCategories[];

$poseLibDefaultArchetypes = {"chars", "props", "sets"};
$poseLibDefaultCasting = {"main", "secondary"};
$poseLibDefaultCharacters = {"default"};
$poseLibDefaultCategories = {"default"};
int $forcePublic = 0;

// -------------------------------------------------------------
// Dependencies:

if (`filetest -d "F:/maya_scripts/my_scripts"` && $forcePublic == 0)
	{
	$poseLibModule = "poseLibModule_001r";
	$poseLibPublicStatus = "[Private]";
	}
else
	{
	$poseLibModule = "barbarian.anim.poseLibModule";
	$poseLibPublicStatus = "[Public]";
	}

print "\nStarting poseLib...";
python("import " + $poseLibModule + " as poseLibModule");
python("reload(poseLibModule)");
print "\nposeLib module successfully loaded.";

$poseLibVersion = python("poseLibModule.getVersion()");
print ("\n-------------------------------------------\nposeLib - version " + $poseLibVersion + "   " + $poseLibPublicStatus + "\n-------------------------------------------\n");

//string $projectsPathsAsString = stringArrayToString($poseLibProjectsPaths, ","); //print ("$projectsPathsAsString = " + $projectsPathsAsString);

// ---------------------------------------------------------------------------------------------------------

//source "F:/maya_scripts/my_scripts/poseLib/poseLib_001m.mel";
//poseLib;

// -----------------------
// Preferences
// -----------------------
global proc poseLibSavePrefs()
	{
	global string $poseLibCurrentProject;
	global string $poseLibProjects[];
	global string $poseLibProjectsPaths[];
	global string $poseLibTextEditor;
	global string $poseLibIconFileFormat;
	global float $poseLibCaptureCameraBGColor[];
	global int $poseLibCaptureCameraFocalLength;
	global float $poseLibCaptureCameraNearClip;
	global float $poseLibCaptureCameraFarClip;
	global float $poseLibIconsBGColor[];
	global float $poseLibPrivateColor[];
	global float $poseLibPublicColor[];
	global int $poseLibDisableIconPreview;
	global int $poseLibUseTexturesForIconPreview;
	global int $poseLibIconsSize[];
	global int $poseLibCustomIconsSize[];
	global int $posesToTheRight;

	waitCursor -state on;

	// Columns widths.
	int $currentWidth = `columnLayout -q -w plOptionsColumn`; // 232
	int $scrollColumnsWidth = `rowColumnLayout -q -w plCharactersCategoriesRCL`; // 226

	// Write the preferences file.
	string $projectsAsString = stringArrayToString($poseLibProjects, ",");
	string $projectsPathsAsString = stringArrayToString($poseLibProjectsPaths, ","); //print ("$projectsPathsAsString = " + $projectsPathsAsString);
	string $characterChoiceAsString = stringArrayToString(`textScrollList -q -si plCharacterChoiceTSL`, ","); //print ("$projectsPathsAsString = " + $projectsPathsAsString);
	string $categoryChoiceAsString = stringArrayToString(`textScrollList -q -si plCategoryChoiceTSL`, ","); //print ("$projectsPathsAsString = " + $projectsPathsAsString);
	print ("$projectsAsString = " + $projectsAsString + "\n"); print ("$projectsPathsAsString = " + $projectsPathsAsString + "\n");
	python("poseLibModule.writeCfgFile(" +
								"\"toto\"," +
								"\"" + $projectsAsString + "\"," +
								"\"" + $projectsPathsAsString + "\"," +
								"\"" + `optionMenu -q -v plArchetypeChoiceOM` + "\"," +
								"\"" + `optionMenu -q -v plCastingChoiceOM` + "\"," +
								"\"" + $characterChoiceAsString + "\"," +
								"\"" + $categoryChoiceAsString + "\"," +
								"\"" + `optionMenu -q -v libraryStatusChoiceOM` + "\"," +
								"\"" + `intSliderGrp -q -v plPoseFractionISG` + "\"," +
								"\"" + $poseLibCurrentProject + "\"," +
								"\"" + $currentWidth + "\"," +
								"\"" + $scrollColumnsWidth + "\"," +
								"\"" + $posesToTheRight + "\"," +
								"\"" + ($poseLibPrivateColor[0] + " " + $poseLibPrivateColor[1] + " " + $poseLibPrivateColor[2]) + "\"," +
								"\"" + ($poseLibPublicColor[0] + " " + $poseLibPublicColor[1] + " " + $poseLibPublicColor[2]) + "\"," +
								"\"" + `textField -q -tx namespaceTextFieldTF` + "\"," +
								"\"" + `radioCollection -q -sl namespaceChoiceRC` + "\"," +
								"\"" + $poseLibIconsSize[0] + "\"," +
								"\"" + $poseLibIconsSize[1] + "\"," +
								"\"" + $poseLibCustomIconsSize[0] + "\"," +
								"\"" + $poseLibCustomIconsSize[1] + "\"," +
								"\"" + ($poseLibIconsBGColor[0] + " " + $poseLibIconsBGColor[1] + " " + $poseLibIconsBGColor[2]) + "\"," +
								"\"" + $poseLibUseTexturesForIconPreview + "\"," +
								"\"" + $poseLibIconFileFormat + "\"," +
								"\"" + $poseLibCaptureCameraFocalLength + "\"," +
								"\"" + $poseLibCaptureCameraNearClip + "\"," +
								"\"" + $poseLibCaptureCameraFarClip + "\"," +
								"\"" + ($poseLibCaptureCameraBGColor[0] + " " + $poseLibCaptureCameraBGColor[1] + " " + $poseLibCaptureCameraBGColor[2]) + "\"," +
								"\"" + $poseLibTextEditor + "\"" +
								")");

	waitCursor -state off;

	evalDeferred("poseLibRefreshPoseList()");
	print "\nSaved poseLib preferences!\n";
	}


global proc poseLibLoadPrefs()
	{
	// Here we declare all the global variables and assign default values to them before loading preferences.

	// Projects:
	global string $poseLibCurrentProject;   // Which project is the current one.
	global string $poseLibProjects[];   // The list of existing projects.
	$poseLibCurrentProject = "Default";
	$poseLibProjects[0] = "Default";

	// Paths:
	global string $poseLibBasePath;     // The path to the root poseLib folder where character folders are located.
	global string $poseLibPrivatePath;  // Path to the user folder (for the Private lib).
	global string $poseLibPublicPath;   // Path to the production folder (for the Public lib).
	global string $poseLibProjectsPaths[];  // The list of all the projects paths (so 2 paths per project).
	global string $mayaImagesPath;
	$poseLibPrivatePath = `workspace -q -fn` + "/poseLib/";
	$poseLibPublicPath = $poseLibPrivatePath;
	$poseLibBasePath = $poseLibPrivatePath;
	$mayaImagesPath = `workspace -q -fn` + "/" + `workspace -q -fileRuleEntry "images"` + "/"; //print ("\nImages directory = " + $mayaImagesPath);
	$poseLibProjectsPaths[0] = $poseLibPrivatePath;
	$poseLibProjectsPaths[1] = $poseLibPublicPath;

	// Text editor:
	global string $poseLibTextEditor;

	if (`about -linux` || `about -linux64`)
		$poseLibTextEditor = "nedit";                                               // Linux
	else if (`about -mac`)
		$poseLibTextEditor = "/Applications/TextEdit.app";                          // Mac
	else if (`about -win`)
		$poseLibTextEditor = "C:/Program Files/Windows NT/Accessories/wordpad.exe"; // PC

	// Options:
	global string $poseLibLibraryStatus;
	global string $poseLibTextEditor;
	global float $poseLibPrivateColor[];
	global float $poseLibPublicColor[];
	global int $poseLibPosesFraction;
	global float $poseLibPrivateColor[];
	global float $poseLibPublicColor[];
	$poseLibPrivateColor = {.6, .8, .5};
	$poseLibPublicColor = {.8, .6, .5};

	// Icons:
	global string $poseLibIconFileFormat;
	global float $poseLibIconsBGColor[];
	global float $poseLibIconsDefaultColor[];
	global int $poseLibIconsSize[];
	global int $poseLibCustomIconsSize[];
	global int $poseLibUseTexturesForIconPreview;
	$poseLibIconFileFormat = ".bmp";
	$poseLibIconsSize = { 104, 82 };
	$poseLibCustomIconsSize = { 104, 82 };
	$poseLibIconsDefaultColor = {.4, .4, .5};
	$poseLibIconsBGColor = $poseLibIconsDefaultColor;
	$poseLibUseTexturesForIconPreview = 1;

	// Camera:
	global float $poseLibCaptureCameraBGColor[];
	global int $poseLibCaptureCameraFocalLength;
	global float $poseLibCaptureCameraNearClip;
	global float $poseLibCaptureCameraFarClip;
	global int $poseLibUseTexturesForIconPreview;
	$poseLibCaptureCameraBGColor = { .75, .75, .75 };

	// Layout:
	global int $posesToTheRight;
	$posesToTheRight = 1;

	// If the .cfg file doesn't exist, then create it based on the default values we just stated.
	// if ( !`filetest -r "F:/maya_scripts/my_scripts/poseLib/poseLib.cfg"` )
	string $userScriptPath = `getenv "BARBARIAN_LOCATION"` + "../commons/config/";
	if (!`filetest -r ($userScriptPath + "poseLib.cfg")`)
		{
		// Make sure we default to private library.
		optionMenu -e -v "Private" libraryStatusChoiceOM;
		poseLibSwitchLibrary();

		poseLibSavePrefs();
		}

	// Read the .cfg file.
	python("poseLibModule.readCfgFile(\"toto\")");

	string $projectsAsString = stringArrayToString($poseLibProjects, ",");
	print ("$projectsAsString = " + $projectsAsString);
	}


// -----------------------
// Misc
// -----------------------
global proc string[] poseLibFilterPoseFiles(string $list[])
	{
	string $filteredList[] = {};

	for ($i=0;$i<`size $list`;$i++)
		{
		if (endsWith($list[$i], ".xml"))
			{
			// Get rid of the file extension.
			string $buffer[];
			int $numTokens = `tokenize $list[$i] "." $buffer`; //print ("\n$buffer[0] = " + $buffer[0]);

			$filteredList[`size $filteredList`] = $buffer[0];
			}
		}

	return $filteredList;
	}


global proc string[] poseLibFilterFolders(string $list[])
	{
	string $filteredList[] = {};

	for ($i=0;$i<`size $list`;$i++)
		{
		if ((`match ".bmp" $list[$i]` != ".bmp") &&
			(`match ".xpm" $list[$i]` != ".xpm") &&
			(`match ".deleted" $list[$i]` != ".deleted") &&
			(`match ".DS_Store" $list[$i]` != ".DS_Store") &&
			(`match ".bak" $list[$i]` != ".bak") &&
			(`match ".cat" $list[$i]` != ".cat") &&
			(`match ".xml" $list[$i]` != ".xml") &&
			(`match "~" $list[$i]` != "\~"))
			{
			$filteredList[`size $filteredList`] = $list[$i];
			}
		}

	return $filteredList;
	}


global proc string poseLibCheckName(string $name)
	{
	// Replace the spaces by "_".
	$name = substituteAllString($name, " ", "_");

	//if (!isValidString($name, "([a-zA-Z]+)([a-zA-Z0-9_])*"))  // Spaces allowed.
	if (!isValidString($name, "([a-zA-Z]+)([a-zA-Z0-9_ ])*"))       // No spaces allowed.
		{
		poseLibMessage("Please don't use any special characters besides \"_\" !");
		return "poseLibReservedNameError";
		}

	return $name;
	}


global proc poseLibBrowseForFolder(string $startFolder)
	{
	global string $poseLibBasePath;

	workspace -dir $startFolder;

	fileBrowserDialog -mode 4
		-fileCommand ("poseLibBrowseForFolderCallback \"" + $poseLibBasePath + "\"")
		-actionName "Choose New poseLib Folder:";
	}


global proc poseLibBrowseForFolderCallback(string $poseLibProjectDoubleClicked, string $poseLibFolderResult, string $type)
	{
	global string $poseLibBasePath;
	global string $poseLibProjectsPaths[];

	string $checkName = `match "[$%\\#@.;?!\"\'\`]" $poseLibFolderResult`;

	if (`size $checkName`)
		error "poseLib: The poseLib path can NOT include any space (\" \") nor either of these: \" $ % \\ # @ . ; ? ! \' \`";

	if ($poseLibFolderResult != $poseLibBasePath)
		{
		string $askToCopyOldPoseLibFolder = `confirmDialog
			-title "New poseLib Folder"
			-message ("Do you want to copy any existing character(s) and categories to the new directory?\n\n(Note: You will have to manually delete the old poseLib folder. This is a safety mesure!)") -ma "center"
			-button "Yes" -button "No, just change the path" -button "Cancel"
			-defaultButton "Yes" -cancelButton "Cancel"
			-dismissString "No"`;

		string $oldPoseLibDefaultPath = $poseLibBasePath; //print ("\nChosen OS path = " + $poseLibFolderResult);

		if (`checkBox -q -v poseLibFolderOptionCB`)
			{
			$poseLibBasePath = $poseLibFolderResult + "/poseLib"; //print ("\nAdding \"/poseLib\" to OS path = " + $poseLibFolderResult + "/poseLib");
			}
		else
			$poseLibBasePath = $poseLibFolderResult;

		if ($askToCopyOldPoseLibFolder == "Yes")
			{
			text -e -l $poseLibBasePath poseLibFolderOptionText;

			string $tmp = toNativePath($oldPoseLibDefaultPath);
			string $tmp2 = toNativePath($poseLibBasePath);

			if (`about -macOS` || `about -linux`)
				system("cp -R " + $tmp + " " + $tmp2);
			else
				system("xcopy " + $tmp + " " + $tmp2 + "/s /e /i");
			}

		else if ($askToCopyOldPoseLibFolder == "No, just change the path")
			text -e -l $poseLibBasePath poseLibFolderOptionText;

		// If the user didn't simply double-click on a project, then add the location to the project list.
		if ($poseLibProjectDoubleClicked != "yes")
			{
			textScrollList -e -appendPosition 1 $poseLibBasePath poseLibDirectoriesProjectsTSL;
			$poseLibProjectsPaths = `textScrollList -q -ai poseLibDirectoriesProjectsTSL`; //print ("Updating $poseLibProjectsPaths[zero] = " + $poseLibProjectsPaths[0] + "\n");
			}

		if ($askToCopyOldPoseLibFolder != "Cancel")
			{
			poseLibSavePrefs();
			evalDeferred("poseLib");
			}

		print ("poseLib: Successfuly changed default path to \"" + $poseLibBasePath + "\".");
		}
	}


global proc poseLibChooseTextEditor()
	{
	global string $poseLibTextEditor;

	if (`window -exists poseLibTextEditorWindow`)
		deleteUI poseLibTextEditorWindow;

	window -tlb off -rtf on -sizeable true -width 200 -height 200  -title "poseLib - Text Editor" poseLibTextEditorWindow;

		frameLayout -mw 5 -bv on -lv off -li 8 -l "Current Text Editor:" -collapsable false -borderStyle "etchedIn" -w 322 poseLibTextEditorFrame;

		columnLayout -adjustableColumn true;
			separator -style "none" -h 4;
			text -l "This is the program used when editing pose files:";
			separator -style "none" -h 4;
			textFieldButtonGrp -bc "poseLibDoChooseTextEditor()" -text $poseLibTextEditor -buttonLabel "..." -cw2 280 30 plTextEditorTFG;
			separator -style "none" -h 4;
			button -l "Apply and Close" -c "poseLibSavePrefs(); poseLib()" -al "left";
		setParent..;

	showWindow poseLibTextEditorWindow;
	}


global proc poseLibDoChooseTextEditor()
	{
	global string $poseLibTextEditor;

	if ( `about -win` )
		{
		string $result = `fileDialog -dm "*.exe"`;
		print ("\n$result = " + $result);

		if ($result != "")
			{
			textFieldButtonGrp -e -ann $result -text $result plTextEditorTFG;
			$poseLibTextEditor = $result;
			}
		else
			textFieldButtonGrp -e -ann $poseLibTextEditor -text $poseLibTextEditor plTextEditorTFG;
		}

	else if ( `about -mac` )
		{
		string $result[] = `fileDialog2 -ds 1 -fm 1 -dir "/Applications"`;
		print "\n$result:"; print $result; print "\n[End of $result]";

		if ($result[0] != "")
			{
			// string $fixedName = `substitute "....$" $result[0] ".app"`;
			textFieldButtonGrp -e -ann $result[0] -text $result[0] plTextEditorTFG;
			$poseLibTextEditor = $result[0];
			}
		else
			textFieldButtonGrp -e -ann $poseLibTextEditor -text $poseLibTextEditor plTextEditorTFG;
		}

	// save the prefs
	poseLibSavePrefs();
	}


global proc poseLibMessage(string $message)
	{
	confirmDialog -title "PoseLib Message" -message $message -button "OK";
	}


global proc poseLibSwitchLibrary()
	{
	global string $poseLibVersion;
	global string $poseLibPrivatePath;
	global string $poseLibPublicPath;
	global string $poseLibBasePath;
	global float $poseLibPrivateColor[];
	global float $poseLibPublicColor[];

	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`; //print ("\n$selectedArchetype = " + $selectedArchetype);
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;

	if (`optionMenu -q -v libraryStatusChoiceOM` == "Public")
		{
		$poseLibBasePath = $poseLibPublicPath;
		optionMenu -e -v "Public" -bgc $poseLibPublicColor[0] $poseLibPublicColor[1] $poseLibPublicColor[2] libraryStatusChoiceOM;
		window -e -title ("poseLib v" + $poseLibVersion + " - " + $poseLibBasePath) poseLibWindow;
		}
	else
		{
		$poseLibBasePath = $poseLibPrivatePath;
		optionMenu -e -v "Private" -bgc $poseLibPrivateColor[0] $poseLibPrivateColor[1] $poseLibPrivateColor[2] libraryStatusChoiceOM;
		window -e -title ("poseLib v" + $poseLibVersion + " - " + $poseLibBasePath) poseLibWindow;
		}
	//print ("\nLibrary status after switching = " + `optionMenu -q -v libraryStatusChoiceOM`);

	poseLibPopulateUIList("archetype");

	string $checkTmp[] = `optionMenu -q -ill plArchetypeChoiceOM`;
	string $allExistingMenuItems[] = {};
	for ($i=0;$i<`size $checkTmp`;$i++)
		{
		$allExistingMenuItems[`size $allExistingMenuItems`] = `menuItem -q -l $checkTmp[$i]`;
		}
	if ( stringArrayContains($selectedArchetype, $allExistingMenuItems) )
		optionMenu -e -v $selectedArchetype plArchetypeChoiceOM;

	poseLibPopulateUIList("casting");

	string $checkTmp[] = `optionMenu -q -ill plCastingChoiceOM`;
	string $allExistingMenuItems[] = {};
	for ($i=0;$i<`size $checkTmp`;$i++)
		{
		$allExistingMenuItems[`size $allExistingMenuItems`] = `menuItem -q -l $checkTmp[$i]`;
		}
	if ( stringArrayContains($selectedCasting, $allExistingMenuItems) )
		optionMenu -e -v $selectedCasting plCastingChoiceOM;

	poseLibPopulateUIList("character");

	string $checkTmp[] = `textScrollList -q -ai plCharacterChoiceTSL`;
	if ( stringArrayContains($selectedCharacter[0], $checkTmp) )
		textScrollList -e -si $selectedCharacter[0] plCharacterChoiceTSL;

	poseLibPopulateUIList("category");

	string $checkTmp[] = `textScrollList -q -ai plCategoryChoiceTSL`;
	if ( stringArrayContains($selectedCategory[0], $checkTmp) )
		textScrollList -e -si $selectedCategory[0] plCategoryChoiceTSL;

	poseLibRefreshPoseList();
	}


// --------------------------
// Display
// --------------------------
global proc poseLibCustomIconsSizeOptions()
	{
	global int $poseLibCustomIconsSize[];

	if (`window -exists poseLibCustomIconsSizeWindow`)
		deleteUI poseLibCustomIconsSizeWindow;

	window -tlb off -rtf on -sizeable true -width 200 -height 32  -title "poseLib - Custom Icons Size" poseLibCustomIconsSizeWindow;
		columnLayout -adjustableColumn true;
			rowColumnLayout -numberOfColumns 4 -columnWidth 1 50 -columnWidth 2 50 -columnWidth 3 50 -columnWidth 4 50;
				text -l "Width:";
				intField -value $poseLibCustomIconsSize[0] -min 32 plCustomIconsWidthIF;
				text -l "Height:";
				intField -value $poseLibCustomIconsSize[1] -min 32 plCustomIconsHeightIF;
			setParent..;

			button -l "Set Custom Size" -c "poseLibSetCustomIconsSize()";

	showWindow poseLibCustomIconsSizeWindow;
	}


global proc poseLibSetCustomIconsSize()
	{
	global int $poseLibIconsSize[];
	global int $poseLibCustomIconsSize[];

	if ( `intField -q -value plCustomIconsWidthIF` < 33 )
		$poseLibCustomIconsSize[0] = 32;

	if ( `intField -q -value plCustomIconsHeightIF` < 33 )
		$poseLibCustomIconsSize[1] = 32;

	$poseLibIconsSize = $poseLibCustomIconsSize;

	evalDeferred("deleteUI poseLibCustomIconsSizeWindow");
	evalDeferred("poseLibSavePrefs()");
	evalDeferred("poseLibRefreshPoseList()");
	}


global proc poseLibSetIconColor(int $control, string $color)
	{
	global string $poseLibBasePath; // The path to the root poseLib folder where character folders are located.
	global float $poseLibIconsBGColor[];

	if ( $color == "Default" )
		shelfButton -e -bgc $poseLibIconsBGColor[0] $poseLibIconsBGColor[1] $poseLibIconsBGColor[2] ("poseButton_" + $control + "_");

	else if ( $color == "Brown" )
		shelfButton -e -bgc 0.500007629511 0.403143358511 0.2 ("poseButton_" + $control + "_");

	else if ( $color == "Red" )
		shelfButton -e -bgc 0.500007629511 0.207064927138 0.0 ("poseButton_" + $control + "_");

	else if ( $color == "Blue" )
		shelfButton -e -bgc 0.254902 0.411765 0.502353 ("poseButton_" + $control + "_");

	else if ( $color == "Green" )
		shelfButton -e -bgc  0.243137 0.50 0.243137 ("poseButton_" + $control + "_");

	// Update the index for all the poses within the visible category.
	string $characterChoice[] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$characterChoice[0] = " + $characterChoice[0]);
	string $categoryChoice[] = `textScrollList -q -si plCategoryChoiceTSL`; //print ("\n$categoryChoice[0] = " + $categoryChoice[0]);
	string $categoryPath = $poseLibBasePath + `optionMenu -q -v plArchetypeChoiceOM` + "/" + `optionMenu -q -v plCastingChoiceOM` + "/" + $characterChoice[0] + "/" + $categoryChoice[0] + "/";
	string $allPoseFiles[] = `getFileList -folder $categoryPath`; //print "\n$allPoseFiles:\n"; print $allPoseFiles; print "\n";
	$allPoseFiles = poseLibFilterPoseFiles($allPoseFiles);//print ("\nList of poses: (" + `size $listOfPoses` + ")\n"); print $listOfPoses; print "\n";

	python("poseLibModule.writeIndexFile(\"" + $categoryPath + "\")");
	}


// --------------------------
// Camera
// --------------------------
global proc poseLibCaptureCameraOptions()
	{
	global float $poseLibCaptureCameraBGColor[];
	global int $poseLibCaptureCameraFocalLength;
	global float $poseLibCaptureCameraNearClip;
	global float $poseLibCaptureCameraFarClip;

	if (`window -exists poseLibCaptureCameraOptionsWindow`)
		deleteUI poseLibCaptureCameraOptionsWindow;

	window -tlb off -rtf on -sizeable true -width 200 -height 32  -title "poseLib - Camera Options" poseLibCaptureCameraOptionsWindow;
		columnLayout -adjustableColumn true;

			colorSliderGrp -label "Capture Camera BG Color" -cw3 130 60 30
				-rgb $poseLibCaptureCameraBGColor[0] $poseLibCaptureCameraBGColor[1] $poseLibCaptureCameraBGColor[2]
				-cc "global float $poseLibCaptureCameraBGColor[]; $poseLibCaptureCameraBGColor = `colorSliderGrp -q -rgb captureCameraBGColorCSG`"
				captureCameraBGColorCSG;

			separator -style "none" -h 8;
			intSliderGrp -label "Focal Length" -cw 1 90 -cw 2 60 -field true
				-minValue 15 -maxValue 300
				-fieldMinValue 15 -fieldMaxValue 300
				-cc "global float $captureCameraFocalLength; $captureCameraFocalLength = `intSliderGrp -q -value captureCameraFocalLengthISG`"
				-value $poseLibCaptureCameraFocalLength captureCameraFocalLengthISG;

			floatSliderGrp -label "Near Clip Plane" -cw 1 90 -cw 2 60 -field true
				-minValue 0.01 -maxValue 10000.0
				-fieldMinValue 0.01 -fieldMaxValue 10000.0
				-cc "global float $captureCameraNearClip; $captureCameraNearClip = `intSliderGrp -q -value captureCameraNearClipFSG`"
				-value $poseLibCaptureCameraNearClip captureCameraNearClipFSG;

			floatSliderGrp -label "Far Clip Plane" -cw 1 90 -cw 2 60 -field true
				-minValue 1.0 -maxValue 100000.0
				-fieldMinValue 1.0 -fieldMaxValue 100000.0
				-cc "global float $captureCameraFarClip; $captureCameraFarClip = `intSliderGrp -q -value captureCameraFarClipFSG`"
				-value $poseLibCaptureCameraFarClip captureCameraFarClipFSG;

			separator -style "none" -h 4;
			button -l "Apply and Close" -c "deleteUI poseLibCaptureCameraOptionsWindow";

	// Connect the sliders to the capture camera if it exists.
	if (`objExists "poseLibCaptureCamera"`)
		{
		connectControl captureCameraFocalLengthISG "poseLibCaptureCameraShape.focalLength";
		connectControl captureCameraNearClipFSG "poseLibCaptureCameraShape.nearClipPlane";
		connectControl captureCameraFarClipFSG "poseLibCaptureCameraShape.farClipPlane";
		}

	showWindow poseLibCaptureCameraOptionsWindow;
	}


// --------------------------
// Archetypes/Casting
// --------------------------
global proc poseLibCreateNewArchetype()
	{
	if (`window -exists createArchetypeWindow`)
		deleteUI createArchetypeWindow;

	window -tlb off -rtf true -s 0 -w 215 -h 53 -title "New Type" createArchetypeWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 175 -l "Name:" -cc "poseLibDoCreateNewArchetype()" -tx "" newArchetypeTFG;
					/*
					rowLayout -nc 2 -cw2 175 160;
						radioCollection forCurrentOrAllCharactersRB;
							radioButton -ann "" -label "For Selected Character Only" -al "left" forCurrentCharacter;
							radioButton -ann "" -label "For Every Character" -al "left" forAllCharacters;
						radioCollection -e -sl forCurrentCharacter forCurrentOrAllCharactersRB;
					setParent..;
					*/
					button -w 100 -l "Cancel" -al "center" -c "deleteUI createArchetypeWindow";

	showWindow createArchetypeWindow;
	}


global proc poseLibDoCreateNewArchetype()
	{
	global string $poseLibBasePath;
	string $allArchetypeDirs[] = `getFileList -folder ($poseLibBasePath + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $newArchetype = `textFieldGrp -q -text newArchetypeTFG`;
	string $checkName = poseLibCheckName($newArchetype);

	print ("\n======> Trying to create a new archetype: " + $poseLibBasePath + "/" + $newArchetype);
	// Check that the name is valid.
	if ($checkName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	// Check that no other archetype with this name already exists.
	for ($archetype in $allArchetypeDirs)
		{
		if ($archetype == $newArchetype)
			{
			poseLibMessage("There is already a type with this name!");
			return;
			}
		}

	sysFile -makeDir ($poseLibBasePath + "/" + $newArchetype);
	sysFile -makeDir ($poseLibBasePath + "/" + $newArchetype + "/primary");
	sysFile -makeDir ($poseLibBasePath + "/" + $newArchetype + "/primary/default");
	sysFile -makeDir ($poseLibBasePath + "/" + $newArchetype + "/primary/default/default");

	evalDeferred("poseLib()");
	evalDeferred("optionMenu -e -v \"" + $newArchetype + "\" plArchetypeChoiceOM");
	evalDeferred("poseLibPopulateUIList(\"casting\")");
	evalDeferred("poseLibPopulateUIList(\"character\")");
	evalDeferred("poseLibPopulateUIList(\"category\")");
	evalDeferred("poseLibRefreshPoseList()");

	print ("poseLib: Created new archetype: " + $newArchetype + "\n");
	}


global proc poseLibRenameArchetype()
	{
	if (`window -exists renameArchetypeWindow`)
		deleteUI renameArchetypeWindow;

	window -tlb off -rtf true -w 10 -h 10 -title "Rename Archetype" renameArchetypeWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 150 -l "Name" -cc "poseLibDoRenameArchetype()" -tx `optionMenu -q -v plArchetypeChoiceOM` renameArchetypeTFG;
					button -l "Cancel" -al "center" -c "deleteUI renameArchetypeWindow";

	showWindow renameArchetypeWindow;
	}


global proc poseLibDoRenameArchetype()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $allArchetypeDirs[] = `getFileList -folder ($poseLibBasePath + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $oldName = $selectedArchetype;
	string $newName = poseLibCheckName(`textFieldGrp -q -tx renameArchetypeTFG`);

	// If it isn't, then display a message and stop there.
	if ($newName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	// Check that no other archetype with this name already exists.
	for ($archetype in $allArchetypeDirs)
		{
		if ($archetype == $newName)
			{
			poseLibMessage("There is already a type with this name!");
			return;
			}
		}

	// Rename the archetype folder.
	sysFile -rename ($poseLibBasePath + $newName) ($poseLibBasePath + $oldName); //print ("\noldName= " + $softArchetypeToRename + "\nnewName= " + ($poseLibBasePath + $newName));

	evalDeferred("poseLib()");
	evalDeferred("optionMenu -e -v \"" + $newName + "\" plArchetypeChoiceOM");
	evalDeferred("optionMenu -e -v \"" + $selectedCasting + "\" plCastingChoiceOM");
	evalDeferred("poseLibRefreshPoseList()");

	print ("Archetype successfully renamed from: " + $poseLibBasePath + $selectedArchetype + " to: " + $newName + "\n");
	}


global proc poseLibDeleteArchetype()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $confirmDeleteWindow = "";

	if ($selectedArchetype != "")
		$confirmDeleteWindow = `confirmDialog -title "Confirm" -message ("Are you sure you want to delete\narchetype \"" + $selectedArchetype + "\" with all the poses inside?") -button "Yes" -button "Cancel" -defaultButton "Yes" -cancelButton "Cancel" -dismissString "Cancel"`;
	else
		poseLibMessage("Please select an archetype in the list first!");

	if ($confirmDeleteWindow == "Yes")
		{
		string $pathTmp = $poseLibBasePath + "/" + $selectedArchetype;

		if (`filetest -w $pathTmp`)
			{
			if ( `filetest -d $pathTmp` )
				{
				sysFile -delete $pathTmp;
				}

			sysFile -rename ($pathTmp + ".deleted") $pathTmp;
			}
		else
			poseLibMessage("\nArchetype folder \"" + $selectedArchetype + "\" is read-only!");

		evalDeferred("poseLib()");

		print ("Archetype successfully deleted: " + $pathTmp + "\n");
		}
	}


global proc poseLibCreateNewCasting()
	{
	if (`window -exists createCastingWindow`)
		deleteUI createCastingWindow;

	window -tlb off -rtf true -s 0 -w 215 -h 53 -title "New Cast" createCastingWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 175 -l "Name:" -cc "poseLibDoCreateNewCasting()" -tx "" newCastingTFG;
					/*
					rowLayout -nc 2 -cw2 175 160;
						radioCollection forCurrentOrAllCharactersRB;
							radioButton -ann "" -label "For Selected Character Only" -al "left" forCurrentCharacter;
							radioButton -ann "" -label "For Every Character" -al "left" forAllCharacters;
						radioCollection -e -sl forCurrentCharacter forCurrentOrAllCharactersRB;
					setParent..;
					*/
					button -w 100 -l "Cancel" -al "center" -c "deleteUI createCastingWindow";

	showWindow createCastingWindow;
	}


global proc poseLibDoCreateNewCasting()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $allCastingDirs[] = `getFileList -folder ($poseLibBasePath + "/" + $selectedArchetype + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $newCasting = `textFieldGrp -q -text newCastingTFG`;
	string $checkName = poseLibCheckName($newCasting);

	// Check the name is valid.
	if ($checkName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	// Check that no other casting with this name already exists.
	for ($casting in $allCastingDirs)
		{
		if ($casting == $newCasting)
			{
			poseLibMessage("There is already a cast with this name!");
			return;
			}
		}

	sysFile -makeDir ($poseLibBasePath + "/" + $selectedArchetype + "/" + $newCasting);
	sysFile -makeDir ($poseLibBasePath + "/" + $selectedArchetype + "/" + $newCasting + "/default");
	sysFile -makeDir ($poseLibBasePath + "/" + $selectedArchetype + "/" + $newCasting + "/default/default");

	evalDeferred("poseLib()");
	evalDeferred("optionMenu -e -v \"" + $selectedArchetype + "\" plArchetypeChoiceOM");
	evalDeferred("optionMenu -e -v \"" + $newCasting + "\" plCastingChoiceOM");
	evalDeferred("poseLibPopulateUIList(\"character\")");

	print ("poseLib: Created new casting: " + $newCasting + "\n");
	}


global proc poseLibRenameCasting()
	{
	if (`window -exists renameCastingWindow`)
		deleteUI renameCastingWindow;

	window -tlb off -rtf true -w 10 -h 10 -title "Rename Casting" renameCastingWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 150 -l "Name" -cc "poseLibDoRenameCasting()" -tx `optionMenu -q -v plCastingChoiceOM` renameCastingTFG;
					button -l "Cancel" -al "center" -c "deleteUI renameCastingWindow";

	showWindow renameCastingWindow;
	}


global proc poseLibDoRenameCasting()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $softCastingToRename = ($poseLibBasePath + $selectedArchetype + "/" + $selectedCasting);
	string $allCastingDirs[] = `getFileList -folder ($poseLibBasePath + "/" + $selectedArchetype + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $newName = poseLibCheckName(`textFieldGrp -q -tx renameCastingTFG`);

	// Check the name is valid.
	if ($newName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	// Check that no other casting with this name already exists.
	for ($casting in $allCastingDirs)
		{
		if ($casting == $newName)
			{
			poseLibMessage("There is already a cast with this name!");
			return;
			}
		}

	sysFile -rename ($poseLibBasePath + $selectedArchetype + "/" + $newName) $softCastingToRename; //print ("\noldName= " + $softArchetypeToRename + "\nnewName= " + ($poseLibBasePath + $newName));

	evalDeferred("poseLib()");
	evalDeferred("optionMenu -e -v \"" + $selectedArchetype + "\" plArchetypeChoiceOM");
	evalDeferred("optionMenu -e -v \"" + $newName + "\" plCastingChoiceOM");
	evalDeferred("poseLibRefreshPoseList()");

	print ("Casting successfully renamed from: " + $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + " to: " + $newName + "\n");
	}


global proc poseLibDeleteCasting()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $confirmDeleteWindow = "";

	if ($selectedCasting != "")
		$confirmDeleteWindow = `confirmDialog -title "Confirm" -message ("Are you sure you want to delete\ncasting \"" + $selectedCasting + "\" with all the poses inside?") -button "Yes" -button "Cancel" -defaultButton "Yes" -cancelButton "Cancel" -dismissString "Cancel"`;
	else
		poseLibMessage("Please select a casting in the list first!");

	if ($confirmDeleteWindow == "Yes")
		{
		string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting;

		if (`filetest -w $pathTmp`)
			{
			if ( `filetest -d $pathTmp` )
				{
				sysFile -delete $pathTmp;
				}

			sysFile -rename ($pathTmp + ".deleted") $pathTmp;
			}
		else
			poseLibMessage("\nCasting folder \"" + $selectedCasting + "\" is read-only!");

		evalDeferred("poseLib()");
		evalDeferred("optionMenu -e -v \"" + $selectedArchetype + "\" plArchetypeChoiceOM");

		print ("Casting successfully deleted: " + $pathTmp + "\n");
		}
	}


// --------------------------
// Characters/Categories
// --------------------------
global proc poseLibCreateNewCharacter()
	{
	if (`window -exists createCharacterWindow`)
		deleteUI createCharacterWindow;

	window -tlb off -rtf true -s 0 -w 215 -h 53 -title "New Character" createCharacterWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 175 -l "Name:" -cc "poseLibDoCreateNewCharacter()" -tx "" newCharacterTFG;
					/*
					rowLayout -nc 2 -cw2 175 160;
						radioCollection forCurrentOrAllCharactersRB;
							radioButton -ann "" -label "For Selected Character Only" -al "left" forCurrentCharacter;
							radioButton -ann "" -label "For Every Character" -al "left" forAllCharacters;
						radioCollection -e -sl forCurrentCharacter forCurrentOrAllCharactersRB;
					setParent..;
					*/
					button -w 100 -l "Cancel" -al "center" -c "deleteUI createCharacterWindow";

	showWindow createCharacterWindow;
	}


global proc poseLibDoCreateNewCharacter()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $allCharDirs[] = `getFileList -folder ($poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $newCharacter = `textFieldGrp -q -text newCharacterTFG`;
	string $checkName = poseLibCheckName($newCharacter);

	// If it isn't, then display a message and stop there.
	if ($checkName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	// Check that no other character with this name already exists.
	for ($character in $allCharDirs)
		{
		if ($character == $newCharacter)
			{
			poseLibMessage("There is already a character with this name!");
			return;
			}
		}

	sysFile -makeDir ($poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/" + $newCharacter);
	sysFile -makeDir ($poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/" + $newCharacter + "/default");
	evalDeferred("poseLib()");
	//evalDeferred("poseLibCharCatManager()");
	evalDeferred("textScrollList -e -si \"" + $newCharacter + "\" plCharacterChoiceTSL");
	//evalDeferred("poseLibOptionsRefreshCategoryList()");
	evalDeferred("poseLibPopulateUIList(\"category\")");

	print ("poseLib: Created new character: " + $newCharacter + "\n");
	}


global proc poseLibRenameCharacter()
	{
	if (`window -exists renameCharacterWindow`)
		deleteUI renameCharacterWindow;

	window -tlb off -rtf true -w 10 -h 10 -title "Rename Character" renameCharacterWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 150 -l "Name" -cc "poseLibDoRenameCharacter()" -tx `textScrollList -q -si plCharacterChoiceTSL` renameCharacterTFG;
					button -l "Cancel" -al "center" -c "deleteUI renameCharacterWindow";

	showWindow renameCharacterWindow;
	}


global proc poseLibDoRenameCharacter()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $allCharDirs[] = `getFileList -folder ($poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $oldName = $selectedCharacter[0];
	string $newName = poseLibCheckName(`textFieldGrp -q -tx renameCharacterTFG`);

	// If it isn't, then display a message and stop there.
	if ($newName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	// Check that no other character with this name already exists.
	for ($character in $allCharDirs)
		{
		if ($character == $newName)
			{
			poseLibMessage("There is already a character with this name!");
			return;
			}
		}

	// Rename the character folder.
	string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/";
	//print ("\nRenaming: " + ($pathTmp + $oldName) + " to: " + ($pathTmp + $newName));
	sysFile -rename ($pathTmp + $newName) ($pathTmp + $oldName);

	evalDeferred("poseLib()");
	evalDeferred("optionMenu -e -v \"" + $selectedArchetype + "\" plArchetypeChoiceOM");
	evalDeferred("optionMenu -e -v \"" + $selectedCasting + "\" plCastingChoiceOM");
	evalDeferred("textScrollList -e -si \"" + $newName + "\" plCharacterChoiceTSL");
	evalDeferred("textScrollList -e -si \"" + $selectedCategory[0] + "\" plCategoryChoiceTSL");

	print ("Character successfully renamed from: " + $poseLibBasePath + "/" + $oldName + " to: " + $newName + "\n");
	}


global proc poseLibDeleteCharacter()
	{
	global string $poseLibBasePath;
	string $confirmDeleteWindow = "";

	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[] = `textScrollList -q -si plCharacterChoiceTSL`;

	if ($selectedCharacter[0] != "")
		$confirmDeleteWindow = `confirmDialog -title "Confirm" -message ("Are you sure you want to delete\ncharacter \"" + $selectedCharacter[0] + "\" with all the poses inside?") -button "Yes" -button "Cancel" -defaultButton "Yes" -cancelButton "Cancel" -dismissString "Cancel"`;
	else
		poseLibMessage("Please select a character in the list first!");

	if ($confirmDeleteWindow == "Yes")
		{
		string $pathTmp = $poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/";

		if (`filetest -w ($pathTmp + $selectedCharacter[0])`)
			{
			if ( `filetest -d $pathTmp` )
				{
				sysFile -delete $pathTmp;
				}

			sysFile -rename ($pathTmp + $selectedCharacter[0] + ".deleted") ($pathTmp + $selectedCharacter[0]);
			}
		else
			poseLibMessage("\nCharacter folder \"" + $selectedCharacter[0] + "\" is read-only!");

		evalDeferred("poseLib()");

		print ("Character successfully deleted: " + $pathTmp + "/" + $selectedCharacter[0] + "\n");
		}
	}


global proc poseLibCreateNewCategory()
	{
	if (`window -exists createCategoryWindow`)
		deleteUI createCategoryWindow;

	window -tlb off -rtf true -s 0 -w 215 -h 53 -title "New Category" createCategoryWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 175 -l "Name:" -cc "poseLibDoCreateNewCategory()" -tx "" newCategoryTFG;

					rowLayout -nc 2 -cw2 175 160;

						radioCollection forCurrentOrAllCharactersRB;
							radioButton -ann "" -label "For Selected Character Only" -al "left" forCurrentCharacter;
							radioButton -ann "" -label "For Every Character" -al "left" forAllCharacters;
						radioCollection -e -sl forCurrentCharacter forCurrentOrAllCharactersRB;
					setParent..;

					button -w 100 -l "Cancel" -al "center" -c "deleteUI createCategoryWindow";

	showWindow createCategoryWindow;
	}


global proc poseLibDoCreateNewCategory()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$selectedCharacter[0] = " + $selectedCharacter[0]);
	string $allCatDirs[] = `getFileList -folder ($poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $newCategory = `textFieldGrp -q -text newCategoryTFG`;
	string $checkName = poseLibCheckName($newCategory);

	// Check that the name is valid.
	if ($checkName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	// Check that no other category with this name already exists.
	for ($category in $allCatDirs)
		{
		if ($category == $newCategory)
			{
			poseLibMessage("There is already a category with this name!");
			return;
			}
		}

	sysFile -makeDir ($poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $newCategory);
	evalDeferred("poseLib()");
	evalDeferred("optionMenu -e -v \"" + $selectedArchetype + "\" plArchetypeChoiceOM");
	evalDeferred("optionMenu -e -v \"" + $selectedCasting + "\" plCastingChoiceOM");
	evalDeferred("textScrollList -e -si \"" + $selectedCharacter[0] + "\" plCharacterChoiceTSL");
	evalDeferred("poseLibPopulateUIList(\"category\")");
	evalDeferred("textScrollList -e -si \"" + $newCategory + "\" plCategoryChoiceTSL");

	print ("New category successfully created: " + $newCategory + "\n");
	}


global proc poseLibRenameCategory()
	{
	if (`window -exists renameCategoryWindow`)
		deleteUI renameCategoryWindow;

	window -tlb off -rtf true -s 0 -w 215 -h 53 -title "Rename Category" renameCategoryWindow;
		columnLayout -adj true;
			frameLayout -mw 2 -lv false -collapsable false -borderStyle "etchedOut";
				columnLayout -adj true;
					textFieldGrp -cw2 50 150 -l "Name" -cc "poseLibDoRenameCategory()" -tx `textScrollList -q -si plCategoryChoiceTSL` renameCategoryTFG;
					button -w 100 -l "Cancel" -al "center" -c "deleteUI renameCategoryWindow";

	showWindow renameCategoryWindow;
	}


global proc poseLibDoRenameCategory()
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $allCatDirs[] = `getFileList -folder ($poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/")`; //print ("\n$allCharDirs="+$allCharDirs[0]);
	string $oldName = $selectedCategory[0];
	string $newName = poseLibCheckName(`textFieldGrp -q -tx renameCategoryTFG`);

	// Make sure the new name is legit.
	$newName = poseLibCheckName($newName); //print ("\n$newName = " + $newName);

	// If it isn't, then display a message and stop there.
	if ($newName == "poseLibReservedNameError")
		return;

	// Check that no other category with this name already exists.
	for ($category in $allCatDirs)
		{
		if ($category == $newName)
			{
			poseLibMessage("There is already a category with this name!");
			return;
			}
		}

	// Rename the category folder.
	string $pathTmp = $poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/";
	sysFile -rename ($pathTmp + $newName) ($pathTmp + $oldName);

	// Relaunch poseLib, the options window and select the new category.
	evalDeferred("poseLib()");
	evalDeferred("optionMenu -e -v \"" + $selectedArchetype + "\" plArchetypeChoiceOM");
	evalDeferred("optionMenu -e -v \"" + $selectedCasting + "\" plCastingChoiceOM");
	evalDeferred("textScrollList -e -si \"" + $selectedCharacter[0] + "\" plCharacterChoiceTSL");
	evalDeferred("textScrollList -e -si \"" + $newName + "\" plCategoryChoiceTSL");

	evalDeferred("print \"Category succesfully renamed from: " + ($pathTmp + $newName) + " to: " + $newName + "\"");
	}


global proc poseLibDeleteCategory()
	{
	global string $poseLibBasePath;
	string $confirmDeleteWindow = "";
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[] = `textScrollList -q -si plCategoryChoiceTSL`;

	if ($selectedCategory[0] != "")
		$confirmDeleteWindow = `confirmDialog -title "Confirm" -message ("Are you sure you want to delete\ncategory \"" + $selectedCategory[0] + "\" with all the poses inside?") -button "Yes" -button "Cancel" -defaultButton "Yes" -cancelButton "Cancel" -dismissString "Cancel"`;
	else
		poseLibMessage("Please select a category in the list first!");

	if ($confirmDeleteWindow == "Yes")
		{
		string $pathTmp = $poseLibBasePath + "/" + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/";

		if (`filetest -w ($pathTmp + $selectedCategory[0])`)
			{
			if ( `filetest -d $pathTmp` )
				{
				sysFile -delete $pathTmp;
				}

			sysFile -rename ($pathTmp + $selectedCategory[0] + ".deleted") ($pathTmp + $selectedCategory[0]);
			}
		else
			poseLibMessage("Category folder \"" + $selectedCategory[0] + "\" is read-only!");

		evalDeferred("poseLib()");

		print ("poseLib: Deleted category: " + $pathTmp + "/" + $selectedCategory[0] + "\n");
		}
	}


// --------------------------
// Projects
// --------------------------
global proc poseLibProjectOptions(string $project)
	{
	global string $poseLibProjects[];
	global string $poseLibProjectsPaths[];

	// ------------------
	// Build UI layout
	// ------------------
	if (`window -exists poseLibProjectOptionsWindow`)
		deleteUI poseLibProjectOptionsWindow;

	window -tlb off -rtf on -sizeable true -width 200 -height 350  -title "poseLib - Project Options" poseLibProjectOptionsWindow;

	// Paths:
	frameLayout -lv off -collapsable false -borderVisible off -w 322 poseLibFolderOptionsFL;
		columnLayout -adjustableColumn true -p poseLibFolderOptionsFL mainColumn3;
			separator -style "none" -h 2;

			rowColumnLayout -nc 5 -cw 1 62 -cw 2 100 -cw 3 4 -cw 4 75 -cw 5 75;
				text -al "right" -l "Project:";
				text -al "center" -fn "smallBoldLabelFont" -l $project projectEditingText;
				separator -style "none" -h 4;
				button -l "Rename" -c "poseLibRenameProject()";
				button -l "Delete" -c "poseLibDeleteProject()";
			setParent..;

			frameLayout -mw 5 -bv on -lv off -li 8 -collapsable false -borderStyle "etchedIn";
				textFieldGrp -cw2 65 241 -l "Private Path:" /*-cc "poseLibValidateProject()"*/ -text "" -ann "" plOptionsPrivatePathTFG;
				textFieldGrp -cw2 65 241 -l "Public Path:" /*-cc "poseLibValidateProject()"*/ -text "" -ann "" plOptionsPublicPathTFG;
			setParent..;

		button -l "Apply!" -h 24 -c ("poseLibValidateProject(\"" + $project + "\"); poseLibSavePrefs(); poseLib()");
		setParent..;

	// Set the paths.
	for ($i=0;$i<`size $poseLibProjects`;$i++)
		{
		if ( $poseLibProjects[$i] == $project )
			{
			textFieldGrp -e /*-cc "poseLibValidateProject()"*/ -text $poseLibProjectsPaths[(($i+1)*2) - 2] -ann "" plOptionsPrivatePathTFG;
			textFieldGrp -e /*-cc "poseLibValidateProject()"*/ -text $poseLibProjectsPaths[(($i+1)*2) - 1] -ann "" plOptionsPublicPathTFG;
			break;
			}
		}

	showWindow poseLibProjectOptionsWindow;
	}


global proc poseLibCreateNewProject()
	{
	if (`window -ex poseLibCreateNewProjectWindow`)
		deleteUI  poseLibCreateNewProjectWindow;

	window -tlb off -rtf on -sizeable true -width 200 -height 50 -title "poseLib - Create New Project" poseLibCreateNewProjectWindow;
		columnLayout -adjustableColumn true;

			frameLayout -mw 5 -bv on -lv off -li 8 -collapsable false -borderStyle "etchedIn";
				text "Note: The private and public paths will be linked to the project name.";
				text "If you don't need to share libraries over a network you can leave the public path blank.";
				textFieldGrp -cw2 100 200 -l "Project Name:" -text "NewProject" plCreateProjectTFG;
				textFieldGrp -cw2 100 200 -l "Private Path:" -text "" plNewPrivatePathTFG;
				textFieldGrp -cw2 100 200 -l "Public Path:" -text "" plNewPublicPathTFG;
			separator -style "none" -h 1;

			setParent..;

			separator -style "none" -h 4;
			button -w 320 -al "center" -l "Create" -c "poseLibDoCreateNewProject";
			button -w 320 -c "deleteUI poseLibCreateNewProjectWindow" -al "center" -l "Close";
		setParent..;

	showWindow poseLibCreateNewProjectWindow;
	}


global proc poseLibDoCreateNewProject()
	{
	global string $poseLibPrivatePath;
	global string $poseLibPublicPath;
	global string $poseLibBasePath;
	global string $poseLibProjects[];
	global string $poseLibProjectsPaths[];

	//print "\nYep?";

	// Make sure the new name is legit.
	string $newProjectName = poseLibCheckName(`textFieldGrp -q -tx plCreateProjectTFG`);
	$newProjectName = poseLibCheckName($newProjectName); //print ("\n$newProjectName = " + $newProjectName);

	// If it isn't, then display a message and stop there.
	if ($newProjectName == "poseLibReservedNameError")
		{
		poseLibMessage("This name contains invalid characters!");
		return;
		}

	// If the new project name already exists, then tell the user.
	if (stringArrayContains($newProjectName, $poseLibProjects))
		{
		poseLibMessage("This poseLib project name already exists!");
		return;
		}
	//print "\nYep!";

	string $newPrivatePath = fromNativePath(`textFieldGrp -q -tx plNewPrivatePathTFG`);
	textFieldGrp -e -tx $newPrivatePath plNewPrivatePathTFG;

	if (!endsWith($newPrivatePath, "/") && $newPrivatePath != "")
		$newPrivatePath = $newPrivatePath + "/";
	print ("\n$newPrivatePath = " + $newPrivatePath);

	if (!`filetest -d $newPrivatePath`)
		{
		print "\nPrivate path does not point to a valid folder!";
		return;
		}

	string $newPublicPath = fromNativePath(`textFieldGrp -q -tx plNewPublicPathTFG`);

	if ($newPublicPath == "")
		$newPublicPath = $newPrivatePath;
	else if (!endsWith($newPublicPath, "/") && $newPublicPath != "")
		$newPublicPath = $newPublicPath + "/";
	print ("\n$newPublicPath = " + $newPublicPath);

	if (!`filetest -d $newPublicPath` && $newPublicPath != "")
		{
		print "\nPublic path does not point to a valid folder!";
		return;
		}

	// At this point, all conditions are filled to create the new project.
	$poseLibProjects[`size $poseLibProjects`] = $newProjectName;
	$poseLibProjectsPaths[`size $poseLibProjectsPaths`] = $newPrivatePath;
	$poseLibProjectsPaths[`size $poseLibProjectsPaths`] = $newPublicPath;

	string $poseLibProjectsStatusTmp = stringArrayToString($poseLibProjects, ",");
	optionVar -stringValue poseLibProjectsStatus $poseLibProjectsStatusTmp;
	print ("\nSaved $pathsProjectsStatusTmp: " + $poseLibProjectsStatusTmp + "\n");

	string $pathsProjectsStatusTmp = stringArrayToString($poseLibProjectsPaths, ",");
	optionVar -stringValue pathsProjectsStatus $pathsProjectsStatusTmp;
	print ("\nSaved $pathsProjectsStatusTmp: " + $pathsProjectsStatusTmp + "\n");

	deleteUI poseLibCreateNewProjectWindow;
//	poseLibOptions();
//	evalDeferred("tabLayout -e -sti 3 poseLibOptionsWindowTL");
//	evalDeferred("optionMenu -e -v " + $newProjectName + " plOptionsProjectsChoiceOM");
//	evalDeferred("poseLibOptionsUpdateProjects()");

	print "\nNew poseLib project successfully created!";

	// Create the project folder.
	poseLibSavePrefs();
	poseLib();
	}


global proc poseLibValidateProject(string $projectBeingEdited)
	{
	global string $poseLibVersion;
	global string $poseLibPrivatePath;
	global string $poseLibPublicPath;
	global string $poseLibBasePath;
	global string $poseLibProjects[];
	global string $poseLibProjectsPaths[];

	string $newPrivatePath = fromNativePath(`textFieldGrp -q -tx plOptionsPrivatePathTFG`);

	if (!endsWith($newPrivatePath, "/") && $newPrivatePath != "")
		$newPrivatePath = $newPrivatePath + "/";
	//print ("\n$newPrivatePath = " + $newPrivatePath);

	if (!`filetest -d $newPrivatePath`)
		{
		print "\nPrivate path does not point to a valid directory!";
		return;
		}

	$poseLibPrivatePath = $newPrivatePath;

	string $newPublicPath = `textFieldGrp -q -tx plOptionsPublicPathTFG`;

	if ($newPublicPath == "")
		$newPublicPath = $newPrivatePath;
	else if (!endsWith($newPublicPath, "/") && $newPublicPath != "")
		$newPublicPath = $newPublicPath + "/";
	//print ("\n$newPublicPath = " + $newPublicPath);

	if (!`filetest -d $newPublicPath` && $newPublicPath != "")
		{
		print "\nPublic path does not point to a valid directory!";
		return;
		}

	$poseLibPublicPath = $newPublicPath;

	// At this point, all conditions are filled to edit the project.
	for ($i=0;$i<`size $poseLibProjects`;$i++)
		{
		if ( $poseLibProjects[$i] == $projectBeingEdited )
			{
			$poseLibProjectsPaths[(($i+1)*2) - 2] = $newPrivatePath;
			$poseLibProjectsPaths[(($i+1)*2) - 1] = $newPublicPath;
			break;
			}
		}
	}


global proc poseLibRenameProject()
	{
	if (`window -ex poseLibRenameProjectWindow`)
		deleteUI poseLibRenameProjectWindow;

	window -tlb off -rtf on -sizeable true -width 200 -height 350  -title "poseLib - Rename Project" poseLibRenameProjectWindow;
		columnLayout -adjustableColumn true;

			frameLayout -mw 5 -bv on -lv off -li 8 -collapsable false -borderStyle "etchedIn";
				textFieldGrp -cw2 100 200 -l "Project Name:" -text `text -q -l projectEditingText` plCreateProjectTFG;
			separator -style "none" -h 1;

			setParent..;

			separator -style "none" -h 4;
			button -w 320 -al "center" -l "Rename" -c ("poseLibDoRenameProject(\"" + `text -q -l projectEditingText` + "\")");
			button -w 320 -c "deleteUI poseLibRenameProjectWindow" -al "center" -l "Close";
		setParent..;

	showWindow poseLibRenameProjectWindow;
	}


global proc poseLibDoRenameProject(string $oldName)
	{
	global string $poseLibProjects[];

	// Make sure the new name is legit.
	string $newProjectName = poseLibCheckName(`textFieldGrp -q -tx plCreateProjectTFG`);
	$newProjectName = poseLibCheckName($newProjectName); //print ("\n$newProjectName = " + $newProjectName);

	// If it isn't, then display a message and stop there.
	if ($newProjectName == "poseLibReservedNameError")
		{
		warning "\nIllegal characters!";
		return;
		}

	string $poseLibProjectsStatusTmp = stringArrayToString($poseLibProjects, ",");
	//optionVar -stringValue poseLibProjectsStatus $poseLibProjectsStatusTmp; //print ("\nSaved $pathsProjectsStatusTmp: " + $pathsProjectsStatusTmp + "\n");

	deleteUI poseLibRenameProjectWindow;

	for ($i=0;$i<`size $poseLibProjects`;$i++)
		{
		if ( $poseLibProjects[$i] == $oldName )
			{
			$poseLibProjects[$i] = $newProjectName;
			break;
			}
		}

	evalDeferred("poseLibSwitchProject(\"" + $newProjectName + "\")");
	evalDeferred("poseLibSavePrefs()");
	evalDeferred("poseLib()");
	evalDeferred("poseLibProjectOptions(\"" + $newProjectName + "\")");
	}


global proc poseLibDeleteProject()
	{
	global string $poseLibProjects[];
	global string $poseLibProjectsPaths[];

	string $result = `confirmDialog -title "PoseLib Confirmation" -message "Are you sure you want to delete this project?" -button "Yes" -button "Cancel" -cancelButton "Cancel"`;

	if ($result == "Cancel" || `size $poseLibProjects` == 1)
		return;

//	int $tmp = (`optionMenu -q -sl plOptionsProjectsChoiceOM`*2) - 2; //print ("\n$tmp = " + $tmp);
//	$poseLibProjectsPaths[$tmp+1] = "";
//	$poseLibProjectsPaths[$tmp] = "";

//	int $tmp = `optionMenu -q -sl plOptionsProjectsChoiceOM` - 1;
//	$poseLibProjects[$tmp] = "";

	// Remove deleted project from $poseLibProjects.
	string $poseLibProjectsTmp[] = {};
	int $projectIndexTmp = 0;

	for ($i=0;$i<`size $poseLibProjects`;$i++)
		{
		if ($poseLibProjects[$i] != `text -q -l projectEditingText`)
			{
			$poseLibProjectsTmp[`size $poseLibProjectsTmp`] = $poseLibProjects[$i];
			}
		else
			{
			$projectIndexTmp = $i;
			}
		}
	print ("\n$poseLibProjectsTmp = " + stringArrayToString($poseLibProjectsTmp, ","));
	$poseLibProjects = $poseLibProjectsTmp;

	// Remove deleted project paths from $poseLibProjectsPaths.
	string $poseLibProjectsPathsTmp[];
	
	for ($i=0;$i<`size $poseLibProjectsPaths`;$i = $i + 2)
		{
		if ($i != $projectIndexTmp * 2)
			{
			$poseLibProjectsPathsTmp[`size $poseLibProjectsPathsTmp`] = $poseLibProjectsPaths[$i];
			$poseLibProjectsPathsTmp[`size $poseLibProjectsPathsTmp`] = $poseLibProjectsPaths[$i + 1];
			}
		}
	print ("\n$poseLibProjectsPathsTmp = " + stringArrayToString($poseLibProjectsPathsTmp, ","));
	$poseLibProjectsPaths = $poseLibProjectsPathsTmp;

//	$poseLibProjectsPaths[$tmp+1] = "";
//	$poseLibProjectsPaths[$tmp] = "";

	string $poseLibProjectsStatusTmp = stringArrayToString($poseLibProjects, ",");
	optionVar -stringValue poseLibProjectsStatus $poseLibProjectsStatusTmp; //print ("\nSaved $pathsProjectsStatusTmp: " + $pathsProjectsStatusTmp + "\n");

	string $pathsProjectsStatusTmp = stringArrayToString($poseLibProjectsPaths, ",");
	optionVar -stringValue pathsProjectsStatus $pathsProjectsStatusTmp; //print ("\nSaved $pathsProjectsStatusTmp: " + $pathsProjectsStatusTmp + "\n");

	string $poseLibProjectsStatusTmp = `optionVar -q poseLibProjectsStatus`;
	$poseLibProjects = stringToStringArray($poseLibProjectsStatusTmp, ","); //print "\n$poseLibProjectsPaths = "; print $poseLibProjectsPaths; print "\n";

	string $pathsProjectsStatusTmp = `optionVar -q pathsProjectsStatus`;
	$poseLibProjectsPaths = stringToStringArray($pathsProjectsStatusTmp, ","); //print "\n$poseLibProjectsPaths = "; print $poseLibProjectsPaths; print "\n";

	$poseLibCurrentProject = $poseLibProjects[0];

	poseLibSavePrefs();
	poseLib();
//	evalDeferred("poseLibOptions()");
//	evalDeferred("tabLayout -e -sti 3 poseLibOptionsWindowTL");
//	evalDeferred("poseLibOptionsUpdateProjects()");
	}


global proc poseLibSwitchProject(string $project)
	{
	global string $poseLibPrivatePath;
	global string $poseLibPublicPath;
	global string $poseLibCurrentProject;
	global string $poseLibProjects[];
	global string $poseLibProjectsPaths[];

	$poseLibCurrentProject = $project;
	print ("\n=====> $poseLibCurrentProject = " + $poseLibCurrentProject);
	for ($i=0;$i<`size $poseLibProjects`;$i++)
		{
		if ( $poseLibProjects[$i] == $project )
			{
			$poseLibPrivatePath = $poseLibProjectsPaths[(($i+1)*2) - 2];
			$poseLibPublicPath = $poseLibProjectsPaths[(($i+1)*2) - 1];
			break;
			}
		}

	poseLibSavePrefs();
	}


// --------------------------
// Poses creation
// --------------------------
global proc poseLibCreateIcon()
	{
	global int $poseLibIconsSize[];
	global float $poseLibCaptureCameraBGColor[];
	global string $poseLibIconFileFormat;
	global string $mayaImagesPath;

	// Get the tmp icon path.
	string $ws = `workspace -q -fullName`; print ("\nProject = " + $ws);
//	string $imagesDir = `workspace -q -renderTypeEntry "images"`; print ("\nImages directory = " + $imagesDir);
	string $imagesDir = `workspace -q -fileRuleEntry "images"`; print ("\nImages directory = " + $imagesDir);
	string $currentImagePath = "";

	// This is in the case the "images" directory path is an absolute path (e.g.: C:/blah...).
	string $buffer[];
	int $numTokens = `tokenize $imagesDir ":" $buffer`; //print ($buffer[`size $buffer`-1]);

	// The path is absolute...
	if ($buffer[1] == "")
		{
		$currentImagePath = $ws + "/" + $imagesDir + "/"; //print ("\n$currentImagePath = " + $currentImagePath);
		}
	else
		{
		// The path is relative...
		$currentImagePath = $imagesDir + "/"; //print ("\n$currentImagePath = " + $currentImagePath);
		}
	print ("\n$currentImagePath = " + $currentImagePath);
	$mayaImagesPath = $currentImagePath;

	// Set the BG color.
	float $currentBGColor[] = `displayRGBColor -q background`;
	displayRGBColor "background" $poseLibCaptureCameraBGColor[0] $poseLibCaptureCameraBGColor[1] $poseLibCaptureCameraBGColor[2];

	// Here we set the default hardware render globals resolution and aspect ratio.
	setAttr -type "string" defaultHardwareRenderGlobals.filename "iconTmp";
	setAttr -type "string" defaultHardwareRenderGlobals.resolution ($poseLibIconsSize[0] + "x" + $poseLibIconsSize[1] + " " + $poseLibIconsSize[0] + " " + $poseLibIconsSize[1] + " " + ($poseLibIconsSize[1]/$poseLibIconsSize[0]));

	// Do the render.
	int $frameNumber = `currentTime -q`;

	// Here we resize the gl frame to match the correct render resolution.
	frameLayout -e -m on -width ($poseLibIconsSize[0]+2) -height ($poseLibIconsSize[1]+2) glRenderFrame;
	rowLayout -e -cw 2 ($poseLibIconsSize[0]+2) iconCaptureRL;

	// Look through the poseLib camera we just created.
	glRenderEditor -e -lt poseLibCaptureCamera hardwareRenderViewBis;

	// Warning: This crashes Maya in certain scenes: 2600/4, or 1402/25, or 702/68.
	glRender -e -fs $frameNumber -accumBufferPasses 4 -transformIcons 0 -edgeSmoothness 1.0 -aam "gaussian" ;
	glRender -rs hardwareRenderViewBis;

	// Put back the background color the way it was.
	displayRGBColor "background" $currentBGColor[0] $currentBGColor[1] $currentBGColor[2];

	print "\nIcon creation done...";
	}


global proc poseLibCreateNewPose(string $ifTextfieldNotEmpty)
	{
	global float $poseLibCaptureCameraBGColor[];
	global int $poseLibIconsSize[];
	global int $poseLibUseTexturesForIconPreview;
	global int $poseLibDisableIconPreview;

	// ------------------------------
	// 1- Create snapshot window UI
	// ------------------------------
	if (`window -exists poseLibCreateNewPoseWindow`)
		deleteUI poseLibCreateNewPoseWindow;

	string $selection[] = `ls -sl`;

	window -rtf true -menuBar false -title "Create New Pose" -w 10 -h 10 -te 300 -le 500 poseLibCreateNewPoseWindow;
		columnLayout -adjustableColumn true -h 50;
			// The way this works is we have 4 columns:
			// 1 is for the capture camera
			// 2 is for the glRender camera
			// 3 is for the icon image
			// 4 is for the buttons frame
			rowLayout -nc 4 -cw4 106 1 1 150 iconCaptureRL;

				// Frame for the capture camera.
				frameLayout -borderStyle "etchedOut" -cl false -cll false -m on -labelVisible false -width 100 -height 100 captureCamFrame;
					if (!`modelPanel -q -ex plCaptureMP`)
						modelPanel -parent captureCamFrame -mbv off -cam poseLibCaptureCamera plCaptureMP;
					else
						modelPanel -e -parent captureCamFrame -mbv off -cam poseLibCaptureCamera plCaptureMP;

					string $barLayout = `modelPanel -q -barLayout plCaptureMP`;
					if ("" != $barLayout && `frameLayout -q -exists $barLayout`)
						{
						frameLayout -e -collapse 1 $barLayout;
						//control -e -m off $barLayout;
						}

					string $modelEditor = `modelPanel -q -me plCaptureMP`;
					modelEditor -e -da "smoothShaded" -grid off -hud off -manipulators off -displayTextures on -ha off -j off -dim off -nc off -wos off -dl "default" $modelEditor;

					setParent ..;
				setParent ..;

				// Frame for the glRender camera.
				frameLayout -borderStyle "etchedOut" -cl false -cll false -m off -labelVisible false /*-width ($poseLibIconsSize[0] + 4) -height ($poseLibIconsSize[1] + 4)*/ glRenderFrame;
					glRenderEditor hardwareRenderViewBis;
				setParent ..;

				// Buttons.
				frameLayout -borderStyle "etchedOut" -labelVisible false -m on -mw 4 -width 190 buttonsFrame;
					columnLayout -adjustableColumn true createNewPoseButtonsCL;
						separator -style "none" -h 2;

							textFieldGrp -cw2 40 134 -l "Name:" -tx $ifTextfieldNotEmpty setNameField;
							separator -style "none" -h 2;

							checkBox -l "Use Textures" -v $poseLibUseTexturesForIconPreview -onc "setAttr defaultHardwareRenderGlobals.texturing 1; $poseLibUseTexturesForIconPreview = 1; optionVar -intValue useTexturesForIconPreviewStatus `checkBox -q -v poseLibUseTexturesForIconPreviewCB`" -ofc "setAttr defaultHardwareRenderGlobals.texturing 0; $poseLibUseTexturesForIconPreview = 0; optionVar -intValue useTexturesForIconPreviewStatus `checkBox -q -v poseLibUseTexturesForIconPreviewCB`" poseLibUseTexturesForIconPreviewCB;
							separator -style "none" -h 2;
							button -label "Create Pose" -en on -c "poseLibCreateIcon(); evalDeferred(\"poseLibDoCreateNewPose\")" createPoseButton;

							separator -style "none" -h 2;

							button -label " Cancel " -c "deleteUI poseLibCreateNewPoseWindow" -h 23;
						setParent ..;
					setParent ..;
				setParent ..;
			setParent ..;

	// -------------------------------------------------
	// 2- Generate a unique camera using current view.
	// -------------------------------------------------
	if (!`objExists poseLibCaptureCamera`)
		{
		string $CurrentPanel  = `getPanel -withFocus`;

		if (`getPanel -to $CurrentPanel` != "modelPanel")   // make sure we've got a camera
			{
			string $visPanel[] = `getPanel -vis`;   // get all visible Panels

			for ($name in $visPanel)
				{
				string $modelPanels[] = `getPanel -type modelPanel`;    // get all modelPanels

				if ((`getPanel -to $name`) == "modelPanel")
					{
					setFocus($name);
					$CurrentPanel = $name;
					break;
					}
				}
			}

		string $CurrentCamera;
		// This is because the stereoCam panel is not a standard camera panel.
		if (catch(`modelPanel -q -cam  $CurrentPanel`))
			$CurrentCamera = "persp";
		else
			$CurrentCamera = `modelPanel -q -cam  $CurrentPanel`;

		float $campos[] = `camera -q -position $CurrentCamera`;
		float $camrot[] = `camera -q -rotation $CurrentCamera`;
		float $camwup[] = `camera -q -worldUp  $CurrentCamera`;
		float $camcoi   = `camera -q -coi      $CurrentCamera`;
		float $focal    = `camera -q -fl       $CurrentCamera`;

		string $camShapeNode = `createNode "camera"`;
		string $camTopNode[] = `listRelatives -p $camShapeNode`;
		rename $camTopNode[0] "poseLibCaptureCamera";
		hide poseLibCaptureCamera;

		int $focalLengthTmp = 200;
		float $nearClipTmp = .1;
		float $farClipTmp = 10000;

		if (`optionVar -exists captureCameraFocalLengthStatus`)
			$focalLengthTmp = `optionVar -q captureCameraFocalLengthStatus`;

		if (`optionVar -exists captureCameraNearClipStatus`)
			$nearClipTmp = `optionVar -q captureCameraNearClipStatus`;

		if (`optionVar -exists captureCameraFarClipStatus`)
			$farClipTmp = `optionVar -q captureCameraFarClipStatus`;

		camera -edit
			//-centerOfInterest $camcoi // removed because it caused the camera to be unruly and lose focus right away in some shots.
			-position $campos[0] $campos[1] $campos[2]
			-rotation $camrot[0] $camrot[1] $camrot[2]
			-worldUp  $camwup[0] $camwup[1] $camwup[2]
			-fl $focalLengthTmp
			-nearClipPlane $nearClipTmp
			-farClipPlane $farClipTmp
		poseLibCaptureCamera;
		}

	// Now look through the right camera in the capture camera model panel.
	modelEditor -e -camera "poseLibCaptureCamera" $modelEditor;
	setAttr "poseLibCaptureCameraShape.displayFilmGate" 1;
	setAttr "poseLibCaptureCameraShape.verticalFilmAperture" 1.1;

	// ---------------------------------------------
	// 3- Hardware render looks thru snapshot camera.
	// ---------------------------------------------
	glRenderEditor -e -lt poseLibCaptureCamera hardwareRenderViewBis;

	// ---------------------------------------------
	// 4- Setup hardware render options.
	// ---------------------------------------------
	// Make sure we're not on a negative frame number.
	int $frame = `currentTime -q`;

	if ($frame < 0)
		{
		currentTime 1;
		$frame = 1;
		poseLibMessage("poseLib doesn't work with negative frame numbers (e.g.: Current time frame has to be 1 or higher). Sorry...");
		}

	// Unlock start and end frame first (as sometimes they get locked (!?)).
	setAttr -l false "defaultHardwareRenderGlobals.startFrame";
	setAttr -l false "defaultHardwareRenderGlobals.endFrame";
	setAttr -l false "defaultHardwareRenderGlobals.byFrame";

	setAttr "defaultHardwareRenderGlobals.startFrame"  $frame;
	setAttr "defaultHardwareRenderGlobals.endFrame"    $frame;
	setAttr "defaultHardwareRenderGlobals.extension"   1;       // 1=xxx.#.ext
	setAttr "defaultHardwareRenderGlobals.backgroundColor" -type double3 0.75 0.75 0.75;

	// Use textures or not?
	if ($poseLibUseTexturesForIconPreview == 1)
		setAttr "defaultHardwareRenderGlobals.texturing" 1;
	else
		setAttr "defaultHardwareRenderGlobals.texturing" 0;

	setAttr "defaultHardwareRenderGlobals.imageFormat" 20;  // bmp=20; jpg=8; tiff=3

	// This is because the first time the window appears, everything gets deselected (?!!).
	//select -r $selection;
	showWindow poseLibCreateNewPoseWindow;
	window -e -w 272 -h `frameLayout -q -height buttonsFrame` poseLibCreateNewPoseWindow;
	print "\nReady to capture icon...";
	}


global proc poseLibDoCreateNewPose()
	{
	global string $poseLibBasePath;
	global string $poseLibPrivatePath;
	global string $poseLibPublicPath;
	global string $poseLibIconFileFormat;
	global string $mayaImagesPath;
	global int $poseLibIconsSize[];
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $selection[] = `ls -l -sl`;
	string $theAttributes[] = {};

	if (size($selection) == 0)
		{
		poseLibMessage("Please select the character's controls first!");
		return;
		}

	//----------------------
	// 1- Get pose name
	//----------------------
	string $name = `textFieldGrp -q -tx setNameField`;
	string $checkName = poseLibCheckName($name);

	// If it isn't, then display a message and stop there.
	if ($checkName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	//---------------------------------
	// 2- Check if file already exists
	//---------------------------------
	string $categoryPath = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";

	// Temp icon created by Maya
	int $frameNumber = `currentTime -q`;
	string $iconTmp = $mayaImagesPath + "iconTmp." + $frameNumber + $poseLibIconFileFormat; print ("\n$iconTmp = " + $iconTmp);
	string $newPoseFile = $categoryPath + $name + ".xml"; print ("\n$newPoseFile = " + $newPoseFile);
	string $newPoseIconFile = $categoryPath + $name + $poseLibIconFileFormat; print ("\n$newPoseIconFile = " + $newPoseIconFile);

	if (`file -q -ex $newPoseFile`)
		{
		string $result = `confirmDialog
			-parent poseLibWindow
			-title "Confirm Save Pose"
			-message ("Overwrite the existing pose: " + $newPoseFile + " ?") -ma "center"
			-button "Yes" -button "No"
			-defaultButton "Yes" -cancelButton "No"
			-dismissString "No"`;

		if ($result == "No")
			return;
		}

	//---------------------------------
	// 3- Write down pose file
	//---------------------------------
	python("poseLibModule.writePose(\"" + $newPoseFile + "\")");

	//--------------------------------------------------------------------
	// 4- Copy icon file from maya default "image" dir to the poselib dir
	//--------------------------------------------------------------------
	// Copy the icon file to its destination.
	int $copyIconResult = catch (`sysFile -copy $newPoseIconFile $iconTmp`);
	if ($copyIconResult)
		poseLibMessage("Could not copy temp icon to " + $newPoseIconFile + " !");

	// Give the ownership to the user; without this, the icon won't show up.
	if (`about -linux` || `about -linux64`)
		{
		string $userTmp = system("whoami");
		evalDeferred(`system("chmod o+w " + $newPoseFile)`);
		evalDeferred(`system("chmod 666 " + $newPoseIconFile)`);
		}

	//---------------------------------
	// 5- Clean-up behind us
	//---------------------------------
	clear $theAttributes;

	// Update the poses.
	poseLibRefreshPoseList();

	// Update the index file.
	string $categoryPath = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";
	python("poseLibModule.writeIndexFile(\"" + $categoryPath + "\")");

	deleteUI poseLibCreateNewPoseWindow;
	select -r $selection;
	print ("\nNew pose successfully created: \"" + $name + "\"");
	}


// --------------------------
// Poses applying
// --------------------------
global proc poseLibApplyPose(int $controlNumber)
	{
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;//print ("\n$poseName=" + $poseName);
	int $altKeyDown = 0;

	// Is the ALT or CTRL key pressed?
	int $getModifiers = `getModifiers`;

	if ( ($getModifiers == 8) || ($getModifiers == 4) )
		$altKeyDown = 1;

	//evalDeferred("poseLibDoApplyPose(\"" + $poseName + "\", " + $altKeyDown + ")");
	poseLibDoApplyPose($poseName, $altKeyDown);
	}


global proc poseLibDoApplyPose(string $poseName, int $altKeyDown)
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $poseFile = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $poseName;  //print ("\n$poseFilePath="+$poseFilePath);
	float $poseLibApplyIncrement = `intSliderGrp -q -v plPoseFractionISG`;

	$startTime = `timerX`;
	waitCursor -state on;

	// Find out what is selected.
	string $sel[] = `ls -sn -sl`;

	//undoInfo -state off;

	// Apply the pose.
	python("poseLibModule.applyPose(\"" + $poseFile + ".xml\", " + $altKeyDown + ", " + (1 - ($poseLibApplyIncrement * .01)) + ")");
	//string $poseToApply[] = `python("poseLibModule.applyPose(\"" + $poseFile + ".xml\", " + $altKeyDown + ", " + (1 - ($poseLibApplyIncrement * .01)) + ")")`;

	//undoInfo -state on;

	//undoInfo -openChunk;

	//for ($attr in $poseToApply)
	//  {
	//  eval($attr);
	//  }

	//undoInfo -closeChunk;

	$totalTime = `timerX -startTime $startTime`;
	//print ("\nTotal time to apply the pose: " + $totalTime + " sec\n");

	// Select back the original selection (in case we deselected a character set when applying the pose).
	//select -r $sel;
	setFocus `getPanel -wf`;

	if ( `waitCursor -q -state` )
		waitCursor -state off;
	}


// --------------------------
// Poses editing
// --------------------------
global proc poseLibSelectPoseControls(int $controlNumber)
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $poseFile = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $poseName; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;

	python("poseLibModule.selectPoseControls(\"" + $poseFile + ".xml\")");
	}


global proc poseLibRenamePose(int $controlNumber)
	{
	if (`window -exists poseLibRenamePoseWindow`)
		deleteUI poseLibRenamePoseWindow;

	string $poseNameTmp = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $poseName = `strip $poseNameTmp`; print ("\n$poseName=" + $poseName);

	window -tlb off -rtf on -w 100 -h 45 -title "Rename Pose" poseLibRenamePoseWindow;
		columnLayout -adj true;
			textFieldGrp -cw2 50 150 -l "Name" -cc ("poseLibDoRenamePose(\"" + $poseName + "\", " + $controlNumber + ")") -tx $poseName setNameField;
			button -w 100 -l "Close" -al "center" -c "deleteUI poseLibRenamePoseWindow";

	if ($poseName != "")
		{
		showWindow poseLibRenamePoseWindow;
		int $poseLibWindowPos[] = `window -q -topLeftCorner poseLibWindow`; //print ("\nleft=" + $poseLibWindowPos[1]);
		window -e -tlc ($poseLibWindowPos[0]+50) ($poseLibWindowPos[1]+50) poseLibRenamePoseWindow;
		}
	else
		poseLibMessage("Please select a pose first!");
	}


global proc poseLibDoRenamePose(string $oldName, int $controlNumber)
	{
	global string $poseLibBasePath;
	global string $poseLibIconFileFormat;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";
	string $newName = poseLibCheckName(`textFieldGrp -q -tx setNameField`);

	// If the new name is not valid, then display a message and stop there.
	if ($newName == "poseLibReservedNameError")
		{
		poseLibMessage("You must enter a valid name, without any special characters!");
		return;
		}

	string $oldPoseFile = $pathTmp + $oldName + ".xml"; //print ("\nOld pose file = " + $oldPoseFile);
	string $oldPoseIcon = $pathTmp + $oldName + ".bmp"; //print ("\nOld icon file = " + $oldPoseIcon);
	string $newPoseFile = $pathTmp + $newName + ".xml"; //print ("\nNew pose file = " + $newPoseFile);
	string $newPoseIcon = $pathTmp + $newName + $poseLibIconFileFormat; //print ("\nNew icon file = " + $newPoseIcon);
	string $result = "";

	if (`file -q -exists $newPoseFile` && ($newName != $oldName))
		{
		setFocus plMainFL;

		$result = `confirmDialog
			-parent poseLibWindow
			-title "Confirm Rename Pose"
			-message "There is already a pose with this name!" -ma "center"
			-button "Overwrite It" -button "Cancel"
			-defaultButton "Overwrite It" -cancelButton "Cancel"
			-dismissString "Cancel"`;
		}

	if ($result == "Overwrite It" || $result == "")
		{
		int $renameIconResult = eval("sysFile -rename \"" + $newPoseIcon + "\" \"" + $oldPoseIcon + "\"");
		int $renamePoseResult = eval("sysFile -rename \"" + $newPoseFile + "\" \"" + $oldPoseFile + "\"");
		evalDeferred("poseLibRefreshPoseList()");
		print ("\nRenamed pose \"" + $oldName + "\" to \"" + $newName + "\"\n");
		}

	string $allPosesIcons[] = evalDeferred("shelfLayout -q -ca plPosesSL");

	for ($i=0;$i<`size $allPosesIcons`;$i++)
		{
		if ( $allPosesIcons[$i] == $newName)
			{
			poseLibRenamePose($i);
			break;
			}
		}
	}


global proc poseLibDeletePose(int $controlNumber)
	{
	global string $poseLibBasePath;
	global string $poseLibIconFileFormat;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;

	setFocus plMainFL;
	string $confirmDeleteResult = `confirmDialog -p poseLibWindow -title "Confirm Delete Pose" -message ("Are you sure you want to delete: \"" + $poseName + "\" ?") -button "Yes" -button "Cancel" -defaultButton "Yes" -cancelButton "Cancel"`;

	if ($confirmDeleteResult == "Yes")
		{
		string $poseToDelete = $pathTmp + $poseName + ".xml"; //print ("\n$poseToDelete = " + $poseToDelete);
		string $iconToDelete = $pathTmp + $poseName + $poseLibIconFileFormat; //print ("\n$iconToDelete = " + $iconToDelete);

		// Actually we rename the file rather than delete it.
		sysFile -rename ($poseToDelete + ".deleted") $poseToDelete;

		// We don't need to delete the icon.
		sysFile -rename ($iconToDelete + ".deleted") $iconToDelete; //print ("\n$Deleted pose: " + $softPoseToDelete);

		// Just get rid of the shelf icon.
		evalDeferred("deleteUI \"poseButton_" + $controlNumber + "_\"");

		// Update the number of poses readout at the top of the icons shelf layout.
		string $numberOfIconsTmp[] = `shelfLayout -q -ca plPosesSL`;
		int $tmp = `frameLayout -q -w plPosesFL` / 2.2;
		frameLayout -e -m on -li $tmp -l ("Poses: " + `size $numberOfIconsTmp`) -fn "smallFixedWidthFont" plPosesFL;

		print ("\nDeleted pose: " + $poseToDelete);
		}
	}


global proc poseLibMovePose(int $controlNumber)
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $categoryPath = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";
	string $allPoseFiles[] = `getFileList -folder $categoryPath`; //print "\n$allPoseFiles:\n"; print $allPoseFiles; print "\n";
	$allPoseFiles = poseLibFilterPoseFiles($allPoseFiles);//print ("\nList of poses: (" + `size $listOfPoses` + ")\n"); print $listOfPoses; print "\n";

	string $poseLibArchetypeList[] = poseLibFilterFolders(`getFileList -folder $poseLibBasePath`);
	string $poseLibCastingList[] = poseLibFilterFolders(`getFileList -folder ($poseLibBasePath + $selectedArchetype + "/")`);
	string $poseLibCharacterList[] = poseLibFilterFolders(`getFileList -folder ($poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/")`);
	string $poseLibCategoryList[] = poseLibFilterFolders(`getFileList -folder ($poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/")`);

	if ($poseName != "")
		{
		if (`window -exists poseLibMovePoseWindow`)
			deleteUI poseLibMovePoseWindow;

		//window -tlb off -w 500 -h 85 -title ("Move Pose -> " + $poseName) poseLibMovePoseWindow;
		window -tlb off -w 500 -h 85 -title "PoseLib Move Poses" poseLibMovePoseWindow;
			columnLayout -adj true;
				text -al "center" "Where do you want to move the pose(s)?";
				separator -style "in" -w 305 -h 10;
			setParent..;

			columnLayout -adj true;
				textScrollList -ams true -nr 8 -h 120 plPosesChoiceTSLtmp;

				for ($i=0;$i<`size $allPoseFiles`;$i++)
					{
					textScrollList -e -a $allPoseFiles[$i] plPosesChoiceTSLtmp;
					}
			setParent..;

			rowColumnLayout -nc 4 -cw 1 100 -cw 2 100 -cw 3 150 -cw 4 150;
				text "Archetypes";
				text "Casting";
				text "Chars";
				text "Categories";
			setParent..;

			rowColumnLayout -nc 4 -cw 1 100 -cw 2 100 -cw 3 150 -cw 4 150;
				textScrollList -ams false -nr 8 -h 120 -sc "poseLibMovePoseUpdateLists(\"casting\")" plArchetypeChoiceTSLtmp;

				for ($archetype in $poseLibArchetypeList)
					{
					textScrollList -e -a $archetype plArchetypeChoiceTSLtmp;
					}

				textScrollList -ams false -nr 8 -h 120 -sc "poseLibMovePoseUpdateLists(\"character\")" plCastingChoiceTSLtmp;

				for ($casting in $poseLibCastingList)
					{
					textScrollList -e -a $casting plCastingChoiceTSLtmp;
					}

				textScrollList -ams false -nr 8 -h 120 -sc "poseLibMovePoseUpdateLists(\"category\")" plCharacterChoiceTSLtmp;

				for ($character in $poseLibCharacterList)
					{
					textScrollList -e -a $character plCharacterChoiceTSLtmp;
					}

				textScrollList -ams false -nr 8 -h 120 plCategoryChoiceTSLtmp;

				for ($i=0;$i<`size $poseLibCategoryList`;$i++)
					{
					textScrollList -e -a $poseLibCategoryList[$i] plCategoryChoiceTSLtmp;
					}
				setParent.. ;

			button -l "Apply" -al "center" -c "poseLibDoMovePose()";

		// Update the lists so that they reflect the main UI choices.
		textScrollList -e -si $selectedArchetype plArchetypeChoiceTSLtmp;
		textScrollList -e -si $selectedCasting plCastingChoiceTSLtmp;
		textScrollList -e -si $selectedCharacter[0] plCharacterChoiceTSLtmp;
		textScrollList -e -si $selectedCategory[0] plCategoryChoiceTSLtmp;
		textScrollList -e -si $poseName plPosesChoiceTSLtmp;

		showWindow poseLibMovePoseWindow;
		int $poseLibWindowPos[] = `window -q -topLeftCorner poseLibWindow`;
		window -e -tlc ($poseLibWindowPos[0]+50) ($poseLibWindowPos[1]+50) poseLibMovePoseWindow;
		}
	else
		poseLibMessage("Please select a pose first!");
	}


global proc poseLibMovePoseUpdateLists(string $data)
	{
	global string $poseLibBasePath;
	global string $poseLibDefaultCasting[];
	global string $poseLibDefaultCharacters[];
	global string $poseLibDefaultCategories[];
	string $selectedArchetype[0] = `textScrollList -q -si plArchetypeChoiceTSLtmp`;
	string $selectedCasting[0] = `textScrollList -q -si plCastingChoiceTSLtmp`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSLtmp`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSLtmp`;

	if ($data == "casting")
		{
		string $castingListTmp[] = `getFileList -folder ($poseLibBasePath + $selectedArchetype[0] + "/")`; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;
		$castingListTmp = poseLibFilterFolders($castingListTmp);

		// If there are no valid casting folders, then create directories for the default ones.
		if (`size $castingListTmp` == 0)
			{
			$castingListTmp = $poseLibDefaultCasting;

			for ($casting in $castingListTmp)
				{
				string $castingPathTmp = $poseLibBasePath + $selectedArchetype[0] + "/" + $casting; //print ("\ncreated " + $softPosesPathTmp);
				sysFile -makeDir $castingPathTmp;
				}
			}

		// Clear the existing casting elements before adding the new ones.
		textScrollList -e -ra plCastingChoiceTSLtmp;

		for ($i=0;$i<`size $castingListTmp`;$i++)
			{
			textScrollList -e -a $castingListTmp[$i] plCastingChoiceTSLtmp;
			}
		textScrollList -e -sii 1 plCastingChoiceTSLtmp;

		poseLibMovePoseUpdateLists("character");
		}

	if ($data == "character")
		{
		string $characterListTmp[] = `getFileList -folder ($poseLibBasePath + $selectedArchetype[0] + "/" + $selectedCasting[0] + "/")`; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;
		$characterListTmp = poseLibFilterFolders($characterListTmp);

		// If there are no valid character folders, then create directories for the default ones.
		if (`size $characterListTmp` == 0)
			{
			$characterListTmp = $poseLibDefaultCharacters;

			for ($character in $characterListTmp)
				{
				string $characterPathTmp = $poseLibBasePath + $selectedArchetype[0] + "/" + $selectedCasting[0] + "/" + $character; //print ("\ncreated " + $softPosesPathTmp);
				sysFile -makeDir $characterPathTmp;
				}
			}

		// Clear the existing casting elements before adding the new ones.
		textScrollList -e -ra plCharacterChoiceTSLtmp;

		for ($i=0;$i<`size $characterListTmp`;$i++)
			{
			textScrollList -e -a $characterListTmp[$i] plCharacterChoiceTSLtmp;
			}
		textScrollList -e -sii 1 plCharacterChoiceTSLtmp;

		poseLibMovePoseUpdateLists("category");
		}

	if ($data == "category")
		{
		string $categoryListTmp[] = `getFileList -folder ($poseLibBasePath + $selectedArchetype[0] + "/" + $selectedCasting[0] + "/" + $selectedCharacter[0] + "/")`; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;
		$categoryListTmp = poseLibFilterFolders($categoryListTmp);

		// If there are no valid character folders, then create directories for the default ones.
		if (`size $categoryListTmp` == 0)
			{
			$categoryListTmp = $poseLibDefaultCategories;

			for ($category in $categoryListTmp)
				{
				string $categoryPathTmp = $poseLibBasePath + $selectedArchetype[0] + "/" + $selectedCasting[0] + "/" + $selectedCharacter[0] + "/" + $category; //print ("\ncreated " + $softPosesPathTmp);
				sysFile -makeDir $categoryPathTmp;
				}
			}

		// Clear the existing casting elements before adding the new ones.
		textScrollList -e -ra plCategoryChoiceTSLtmp;

		for ($i=0;$i<`size $categoryListTmp`;$i++)
			{
			textScrollList -e -a $categoryListTmp[$i] plCategoryChoiceTSLtmp;
			}
		textScrollList -e -sii 1 plCategoryChoiceTSLtmp;
		}
	}


global proc poseLibDoMovePose()
	{
	global string $poseLibBasePath;
	global string $poseLibIconFileFormat;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $selectedArchetypeTmp[0] = `textScrollList -q -si plArchetypeChoiceTSLtmp`;
	string $selectedCastingTmp[0] = `textScrollList -q -si plCastingChoiceTSLtmp`;
	string $selectedCharacterTmp[0] = `textScrollList -q -si plCharacterChoiceTSLtmp`;
	string $selectedCategoryTmp[0] = `textScrollList -q -si plCategoryChoiceTSLtmp`;
	string $selectedPoses[] = `textScrollList -q -si plPosesChoiceTSLtmp`;
	string $destinationPath = $poseLibBasePath + $selectedArchetypeTmp[0] + "/" + $selectedCastingTmp[0] + "/" + $selectedCharacterTmp[0] + "/" + $selectedCategoryTmp[0] + "/"; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;

	for ($i=0;$i<`size $selectedPoses`;$i++)
		{
		string $sourcePose = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $selectedPoses[$i] + ".xml";
		string $sourceIcon = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $selectedPoses[$i] + $poseLibIconFileFormat;
		string $newPose = $destinationPath + $selectedPoses[$i] + ".xml";
		string $newIcon = $destinationPath + $selectedPoses[$i] + $poseLibIconFileFormat;
		//print ("\nMove pose: " + $sourcePose); print ("\nTo: " + $newPose);
		//print ("\nMove icon: " + $sourceIcon); print ("\nTo: " + $newIcon);

		if ( `filetest -r $newPose` )
			poseLibMessage("There is already a pose named: " + $selectedPoses[$i] + " in: " + $destinationPath);
		else
			{
			int $copyPoseResult = evalDeferred("sysFile -copy \"" + $newPose + "\" \"" + $sourcePose + "\"");
			int $copyIconResult = evalDeferred("sysFile -copy \"" + $newIcon + "\" \"" + $sourceIcon + "\"");
			int $renamePoseResult = evalDeferred("sysFile -rename \"" + $sourcePose + ".deleted\" \"" + $sourcePose + "\"");
			int $renameIconResult = evalDeferred("sysFile -rename \"" + $sourceIcon + ".deleted\" \"" + $sourceIcon + "\"");
			}
		}

	evalDeferred("poseLib()");
	}


global proc poseLibMovePoseMiddleMouse(string $poseName, string $shelfButtonName, string $destinationCategory)
	{
	global string $poseLibBasePath;
	global string $poseLibIconFileFormat;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $sourcePath = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/"; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;
	string $destinationPath = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $destinationCategory + "/"; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;

	string $sourcePose = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $poseName + ".xml";
	string $sourceIcon = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $poseName + $poseLibIconFileFormat;
	string $newPose = $destinationPath + $poseName + ".xml"; //print ("\n$newPose = " + $newPose);
	string $newIcon = $destinationPath + $poseName + $poseLibIconFileFormat;

	string $allDestinationPoseFiles[] = `getFileList -folder $destinationPath`; //print "\n$allPoseFiles:\n"; print $allPoseFiles; print "\n";
	$allDestinationPoseFiles = poseLibFilterPoseFiles($allDestinationPoseFiles); //print ("\n$allPoseFiles after filtering: (" + `size $allPoseFiles` + ")\n"); print $allPoseFiles; print "\n";

	float $iconColor[] = `shelfButton -q -bgc $shelfButtonName`;
	string $iconColorAsString = ($iconColor[0] + " " + $iconColor[1] + " " + $iconColor[2]);

	// Check if a pose with that name already exists in the destination category.
	if ( `filetest -r $newPose` )
		{
		string $confirmReplaceWindow = `confirmDialog -p poseLibWindow -title "Confirm Replace Pose" -message ("There is already a pose named: " + $poseName + " in: " + $destinationPath + "\nDo you want to replace it?") -button "Yes" -button "Cancel" -defaultButton "Yes" -cancelButton "Cancel"`;

		if ($confirmReplaceWindow != "Yes")
			return;
		}

	print ("\nMoving: " + $poseName + " to: " + $destinationCategory + "\n");

	// Take care of the files.
	int $copyPoseResult = evalDeferred("sysFile -copy \"" + $newPose + "\" \"" + $sourcePose + "\"");
	int $copyIconResult = evalDeferred("sysFile -copy \"" + $newIcon + "\" \"" + $sourceIcon + "\"");
	int $renamePoseResult = evalDeferred("sysFile -rename \"" + $sourcePose + ".deleted\" \"" + $sourcePose + "\"");
	int $renameIconResult = evalDeferred("sysFile -rename \"" + $sourceIcon + ".deleted\" \"" + $sourceIcon + "\"");

	// Remove the pose from the current index file.
	python("poseLibModule.updateIndexFile(\"remove\", \"" + $sourcePath + "\", \"" + $poseName + "\")");

	// If the index file doesn't exist, create it based on the poses files. Since the moved pose has already been copied, no need to do amything else.
	if ( !`filetest -r ($destinationPath + "posesIndex.pli")` )
		{
		print "\nSince the index doesn't exist, I'm creating it!\n";
		python("poseLibModule.writeIndexFile(\"" + stringArrayToString($allDestinationPoseFiles, " ") + "\")");
		}

	// Now include the new pose at the end of the index file, with its color.
	python("poseLibModule.updateIndexFile(\"add\", \"" + $destinationPath + "\", \"" + $poseName + "\", \"" + $iconColorAsString + "\", \"" + `size $allDestinationPoseFiles` + "\")");

	evalDeferred("poseLibRefreshPoseList()");
	}


global proc poseLibReplacePose(int $controlNumber)
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";

	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $confirmDeleteWindow = "";
	string $selection[] = `ls -l -sl`;

	if (size($selection) == 0)
		{
		poseLibMessage("Please select the character's controls first!");
		return;
		}

	// Get the pose name.
	setFocus plMainFL;
	string $confirmReplaceWindow = `confirmDialog -p poseLibWindow -title "Confirm Replace Pose" -message ("Are you sure you want to replace: \"" + $poseName + "\" ?") -button "Yes" -button "Cancel" -defaultButton "Yes" -cancelButton "Cancel"`;

	if ($confirmReplaceWindow == "Yes")
		{
		// Write down the pose file (no need to change the icon).
		python("poseLibModule.writePose(\"" + $pathTmp + $poseName + ".xml\")");

		// Clean-up behind us.
		//select -r $selection;

		print ("\nReplaced pose: \"" + $poseName + "\"");
		}
	}


global proc poseLibAddReplaceSelectedControls(int $controlNumber)
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $poseFile = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $poseName; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;

	python("poseLibModule.addReplaceControls(\"" + $poseFile + ".xml\")");
	}


global proc poseLibRemoveSelectedControls(int $controlNumber)
	{
	global string $poseLibBasePath;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $poseFile = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/" + $poseName; //print "\n$poseLibCastingList:\n"; print $poseLibCastingList;

	python("poseLibModule.removeControls(\"" + $poseFile + ".xml\")");
	}


global proc poseLibOutputPoseInfo(int $controlNumber)
	{
	global string $poseLibBasePath;
	global string $poseLibTextEditor;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $poseFilePath = $pathTmp + $poseName + ".xml"; print ("\n$poseFilePath = " + $poseFilePath);

	// -------------------
	// Open the file
	// -------------------
	int $fileId = `fopen $poseFilePath "r"`;
	string $controlName = `fgetline $fileId`; // Because first line is empty

	print ("\n\n=============================================\n");
	print ("Pose Control List for \"" + $poseName + "\":\n");
	print ("=============================================\n");

	// Go through all the controls.
	while (size($controlName) > 0)
		{
		$controlName = `fgetline $fileId`;
		print $controlName;
		}
	fclose $fileId;

	print "\nEnd of pose...";
	}


global proc poseLibEditPoseFile(int $controlNumber)
	{
	global string $poseLibBasePath;
	global string $poseLibTextEditor;
	string $selectedArchetype = `optionMenu -q -v plArchetypeChoiceOM`;
	string $selectedCasting = `optionMenu -q -v plCastingChoiceOM`;
	string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$charTmp[0] = " + $charTmp[0]);
	string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;
	string $pathTmp = $poseLibBasePath + $selectedArchetype + "/" + $selectedCasting + "/" + $selectedCharacter[0] + "/" + $selectedCategory[0] + "/";
	string $poseName = `shelfButton -q -l ("poseButton_" + $controlNumber + "_")`;
	string $poseFilePath = $pathTmp + $poseName + ".xml"; //print ("\n$poseFilePath = " + $poseFilePath);

	if (`about -win`)
		system("start " + $poseLibTextEditor + " " + $poseFilePath);
	else if (`about -li`)
		system($poseLibTextEditor + " " + $poseFilePath + " &");
	else if (`about -mac`)
		{
		print ("\nopen -a " + $poseLibTextEditor + " " + $poseFilePath + "\n");
		system("open -a " + $poseLibTextEditor + " " + $poseFilePath);
		}
	}


// ---------------------------
// Interface
// ---------------------------
global proc string[] poseLibDragCallBack(string $dragCtrl, int $x, int $y, int $mods)
	{
	// Get the short name of the control.
	//string $dragCtrlShort = `match "[^|]*$" $dragCtrl`; print ("\n$dragCtrlShort = " + $dragCtrlShort);
	return {};
	}


global proc poseLibDropCallBack(string $dragCtrl, string $dropCtrl, string $msgs[], int $x, int $y, int $type)
	{
	//print ("\n$dragCtrl = " + $dropCtrl);
	//print ("\n$dropCtrl = " + $dropCtrl);
	//print "\n$msgs:"; print $msgs;
	//print ("\n$x = " + $x);
	//print ("\n$y = " + $y);
	//print ("\n$type = " + $type);

	// Get the short name of the controls.
	string $dropCtrlShort = `match "[^|]*$" $dropCtrl`; //print ("\n$dropCtrlShort = " + $dropCtrlShort);
	string $dragCtrlShort = `match "[^|]*$" $dragCtrl`; //print ("\n$dragCtrlShort = " + $dragCtrlShort);

	// If we're dealing with a drop to category:
	if ( $dropCtrlShort == "plCategoryChoiceTSL" )
		{
		int $tslIndex = ceil($y / 14);
		//print ("\n$tslIndex = " + $tslIndex);

		// Now find out which category lives there.
		string $allCategories[] = `textScrollList -q -ai plCategoryChoiceTSL`;
		string $destinationCategory = "";

		if ( $tslIndex > `size $allCategories` )
			{
			//print ("\nWinner is = " + $allCategories[`size $allCategories`-1]);
			$destinationCategory = $allCategories[`size $allCategories`-1];
			}
		else
			{
			//print ("\nWinner is = " + $allCategories[$tslIndex]);
			$destinationCategory = $allCategories[$tslIndex];
			}

		// Finally move the pose to the chosen category.
		poseLibMovePoseMiddleMouse(`shelfButton -q -l $dragCtrlShort`, $dragCtrlShort, $destinationCategory);
		}

	// If we're dealing with a drop shelf button:
	else
		{
		// Locate the index (1-based position) of the drop shelf button in the shelfLayout.
		string $listOfShelfButtons[] = `shelfLayout -q -ca plPosesSL`; //print "\n$listOfShelfButtons:\n"; print $listOfShelfButtons;
		int $newPosition = 0;

		for ($i=0;$i<`size $listOfShelfButtons`;$i++)
			{
			if ( $listOfShelfButtons[$i] == $dropCtrlShort )
				{
				$newPosition = $i;
				break;
				}
			}

		// Finally reposition the drag shelf button.
		shelfLayout -e -pos $dragCtrl ($newPosition+1) plPosesSL;

		// Update the index for all the poses within the visible category.
		global string $poseLibBasePath; // The path to the root poseLib folder where character folders are located.
		string $characterChoice[] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$characterChoice[0] = " + $characterChoice[0]);
		string $categoryChoice[] = `textScrollList -q -si plCategoryChoiceTSL`; //print ("\n$categoryChoice[0] = " + $categoryChoice[0]);
		string $categoryPath = $poseLibBasePath + `optionMenu -q -v plArchetypeChoiceOM` + "/" + `optionMenu -q -v plCastingChoiceOM` + "/" + $characterChoice[0] + "/" + $categoryChoice[0] + "/";
		string $allPoseFiles[] = `getFileList -folder $categoryPath`; //print "\n$allPoseFiles:\n"; print $allPoseFiles; print "\n";
		$allPoseFiles = poseLibFilterPoseFiles($allPoseFiles);//print ("\nList of poses: (" + `size $listOfPoses` + ")\n"); print $listOfPoses; print "\n";

		python("poseLibModule.writeIndexFile(\"" + $categoryPath + "\")");
		}
	}


global proc poseLibChangeOptionsColumnWidth(string $state)
	{
	int $smallestWidthAllowed = 232;
	int $currentWidth = `columnLayout -q -w plOptionsColumn`; // 232
	int $scrollColumnsWidth = `rowColumnLayout -q -w plCharactersCategoriesRCL`; // 226
	int $menusWidth = `rowColumnLayout -q -w characterCategoryRCL`; // 226

	if ( $state == "narrower" )
		{
		if ( ($currentWidth - 25) >= $smallestWidthAllowed )
			{
			columnLayout -e -w ($currentWidth - 25) plOptionsColumn;
			rowColumnLayout -e -nc 2 -cw 1 `trunc(($scrollColumnsWidth-26)/2)` -cw 2 `trunc(($scrollColumnsWidth-26)/2)` plCharactersCategoriesRCL;
			//                                          36                              76                          30                              70
			rowColumnLayout -e -nc 5 -cw 1 `trunc((($menusWidth-25)/6.2))` -cw 2 `trunc((($menusWidth-25)/3))` -cw 3 `trunc((($menusWidth-25)/7.4))` -cw 4 `trunc((($menusWidth-25)/3.2))` -cw 5 2 characterCategoryRCL;
			}
		}
	else if ( $state == "wider" )
		{
		columnLayout -e -w ($currentWidth + 25) plOptionsColumn;
		rowColumnLayout -e -nc 2 -cw 1 `trunc(($scrollColumnsWidth+24)/2)` -cw 2 `trunc(($scrollColumnsWidth+24)/2)` plCharactersCategoriesRCL;
		//                                          36                              76                          30                              70
		rowColumnLayout -e -nc 5 -cw 1 `trunc((($menusWidth+25)/6.2))` -cw 2 `trunc((($menusWidth+25)/3))` -cw 3 `trunc((($menusWidth+25)/7.4))` -cw 4 `trunc((($menusWidth+25)/3.2))` -cw 5 2 characterCategoryRCL;
		}
	}


global proc poseLibRefreshPoseList()
	{
	global string $poseLibStoredPath;
	global string $poseLibBasePath;
	global string $poseLibCategoryList[];
	global string $poseLibIconFileFormat;
	global float $poseLibIconsBGColor[];
	global float $poseLibIconsDefaultColor[];
	global int $poseLibIconsSize[];
	string $characterChoice[] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$characterChoice[0] = " + $characterChoice[0]);
	string $categoryChoice[] = `textScrollList -q -si plCategoryChoiceTSL`; //print ("\n$categoryChoice[0] = " + $categoryChoice[0]);
	string $categoryPath = $poseLibBasePath + `optionMenu -q -v plArchetypeChoiceOM` + "/" + `optionMenu -q -v plCastingChoiceOM` + "/" + $characterChoice[0] + "/" + $categoryChoice[0] + "/";
	string $allPoseFiles[] = `getFileList -folder $categoryPath`; //print "\n$allPoseFiles:\n"; print $allPoseFiles; print "\n";
	$allPoseFiles = poseLibFilterPoseFiles($allPoseFiles); //print ("\n$allPoseFiles after filtering: (" + `size $allPoseFiles` + ")\n"); print $allPoseFiles; print "\n";
	//print ("\n$allPoseFiles after filtering: (" + `size $allPoseFiles` + ")\n"); print $allPoseFiles; print "\n";

	// Delete and recreate the poses shelfLayout (probably faster than deleting the icons one by one).
	deleteUI plPosesSL;
	shelfLayout -p plPosesFL -h 200 -st "iconAndTextVertical" plPosesSL;

	// ------------------------------
	// 1- Convert old poses
	// ------------------------------
	int $needRefresh = 0;

	if ( `filetest -d $categoryPath` )
		$needRefresh = `python("poseLibModule.convertOldPosesToXML(\"" + $categoryPath + "\")")`;

	// If some poses have been converted, then we need to parse the directory again for them to show up.
	if ( $needRefresh )
		{
		$allPoseFiles = `getFileList -folder $categoryPath`; //print "\n$allPoseFiles:\n"; print $allPoseFiles; print "\n";
		$allPoseFiles = poseLibFilterPoseFiles($allPoseFiles);//print ("\nList of poses: (" + `size $listOfPoses` + ")\n"); print $listOfPoses; print "\n";
		}

	// --------------------------------------------
	// 2- Sort poses list into index order
	// --------------------------------------------
	// If the index file doesn't exist and there are valid poses in the directory, create the index file.
	if ( !`filetest -w ($categoryPath + "posesIndex.pli")` && `size $allPoseFiles` > 0 )
		python("poseLibModule.writeIndexFile(\"" + $categoryPath + "\", \"" + stringArrayToString($allPoseFiles, " ") + "\")");

	string $listOfPosesByIndexTmp[] = {};
	string $listOfPosesByIndex[] = {};
	string $listOfIconsBGColors[] = {};

	if ( `size $allPoseFiles` > 0 )
		$listOfPosesByIndexTmp = python("poseLibModule.readIndexFile(\"" + $categoryPath + "\")");

	for ($i=0;$i<`size $listOfPosesByIndexTmp`;$i=$i+2)
		{
		$listOfPosesByIndex[`size $listOfPosesByIndex`] = $listOfPosesByIndexTmp[$i];
		$listOfIconsBGColors[`size $listOfIconsBGColors`] = $listOfPosesByIndexTmp[$i+1];
		}
	//print ("\n$listOfIconsBGColors: (" + `size $listOfIconsBGColors` + ")\n"); print $listOfIconsBGColors; print "\n";

	// Now compare the number of existing poses and the number of indexed poses.
	$listOfPosesByIndex = stringArrayCatenate($listOfPosesByIndex, $allPoseFiles);

	// If some poses are not indexed, then add them at the end of the list.
	$listOfPosesByIndex = stringArrayRemoveDuplicates($listOfPosesByIndex);
	//print ("\n$listOfPosesByIndex: (" + `size $listOfPosesByIndex` + ")\n"); print $listOfPosesByIndex; print "\n";

	// ------------------------------
	// 3- Create new poses buttons
	// ------------------------------
	for ($i=0;$i<`size $listOfPosesByIndex`;$i++)
		{
		//print ("\nLooking for icon: " + $categoryPath + $listOfPosesByIndex[$i] + ".bmp");
		// Convert the bg color from string to float.
		string $iconBgTmp[] = stringToStringArray($listOfIconsBGColors[$i], " ");
		float $bgc0 = 0.4;
		float $bgc1 = 0.4;
		float $bgc2 = 0.5;

		if ($iconBgTmp[0] != "")
			{
			$bgc0 = $iconBgTmp[0];
			$bgc1 = $iconBgTmp[1];
			$bgc2 = $iconBgTmp[2];
			}

		shelfButton
			-style "iconAndTextVertical"
			-bgc $bgc0 $bgc1 $bgc2
			-parent plPosesSL
			-ebg true
			-dgc "poseLibDragCallBack" -dpc "poseLibDropCallBack"
			-label $listOfPosesByIndex[$i]
			-c ("poseLibApplyPose(" + $i + ")") poseButton[$i];

		// In Maya 2012 and up, the shelfButtons get a default popupMenu that we need to delete before ours shows up.
		if (startsWith(`about -version`, "2012") || startsWith(`about -version`, "2013") || startsWith(`about -version`, "2014") || startsWith(`about -version`, "2015") || startsWith(`about -version`, "2016"))
			{
			string $parasiteMenus[] = `shelfButton -q -pma ("poseButton_" + $i + "_")`;
			deleteUI $parasiteMenus[0];
			}

		//Add a popup to each shelfButton so we can edit its parameters, or delete it.
		popupMenu -button 3 posePopupMenu[$i];
			menuItem -en on -label "Select Pose Controls" -c ("poseLibSelectPoseControls (" + $i + ")") poseLibSelectPoseControlsPUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Rename Pose" -c ("poseLibRenamePose(" + $i + ")") poseLibRenamePosePUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Move Pose" -c ("poseLibMovePose(" + $i + ")") poseLibMovePosePUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Replace Pose" -c ("poseLibReplacePose(" + $i + ")") poseLibReplacePosePUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Delete Pose" -c ("poseLibDeletePose(" + $i + ")") poseLibDeletePosePUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Icon Color" -subMenu true;// poseLibSetIconColorPUM[$i];
				menuItem -en on -label "Default" -c ("poseLibSetIconColor(" + $i + ", \"Default\")");
				menuItem -en on -label "Blue" -c ("poseLibSetIconColor(" + $i + ", \"Blue\")");
				menuItem -en on -label "Brown" -c ("poseLibSetIconColor(" + $i + ", \"Brown\")");
				menuItem -en on -label "Green" -c ("poseLibSetIconColor(" + $i + ", \"Green\")");
				menuItem -en on -label "Red" -c ("poseLibSetIconColor(" + $i + ", \"Red\")");
			setParent -menu..;
			menuItem -divider true ;
			menuItem -en on -label "Edit Pose" -subMenu true;
			menuItem -en on -label "Add/Replace Selected Control(s)" -c ("poseLibAddReplaceSelectedControls (" + $i + ")") addReplaceControlsPUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Remove Selected Control(s)" -c ("poseLibRemoveSelectedControls (" + $i + ")") removeControlsPUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Output Pose Info" -c ("python(\"poseLibModule.outputPoseInfo('" + $categoryPath + $listOfPosesByIndex[$i] + ".xml')\")") poseLibSelectPoseControlsPUM[$i];
			menuItem -divider true;
			menuItem -en on -label "Open Pose File With Text Edtor" -c ("poseLibEditPoseFile(" + $i + ")") poseLibSelectPoseControlsPUM[$i];
			setParent ..;
		setParent ..;

		// Assign the icon if it exists.
		if (`filetest -r ($categoryPath + $listOfPosesByIndex[$i] + ".bmp")`)
			shelfButton -e -image1 ($categoryPath + $listOfPosesByIndex[$i] + ".bmp") ("poseButton_" + $i + "_");

		// If there's no icon for the pose, just display a red background.
		else
			{
			shelfButton -e -bgc .9 .2 0 ("poseButton_" + $i + "_");
			print ("\n(Did not find an icon file for \"" + $listOfPosesByIndex[$i] + "\")");
			}
		}

	// Now enforce the cell size (based on the default icon size).
	shelfLayout -e -m on -cw ($poseLibIconsSize[0] + 4) -ch ($poseLibIconsSize[1] + 24) plPosesSL;//print ("\nCell height = " + `shelfLayout -q -ch plPosesSL`);

	// --------------------------
	// 4- Update frame layouts
	// --------------------------
	int $tmp = `frameLayout -q -w plPosesFL` / 2.2;
	frameLayout -e -li $tmp -l ("Poses: " + `size $allPoseFiles`) -fn "smallFixedWidthFont" plPosesFL;
	}


global proc string[] poseLibPopulateUIList(string $data)
	{
	global string $poseLibBasePath;
	global string $poseLibDefaultArchetypes[];  // Defined at the start of the script.
	global string $poseLibDefaultCasting[];     // Defined at the start of the script.
	global string $poseLibDefaultCharacters[];  // Defined at the start of the script.
	global string $poseLibDefaultCategories[];  // Defined at the start of the script.

	if ($data == "archetype")
		{
		string $poseLibArchetypeList[] = poseLibFilterFolders(`getFileList -folder $poseLibBasePath`);

		// If there are no valid archetypes folders, then create directories for the default ones.
		if (`size $poseLibArchetypeList` == 0)
			{
			$poseLibArchetypeList = $poseLibDefaultArchetypes;

			for ($archetype in $poseLibArchetypeList)
				{
				string $archetypesPathTmp = $poseLibBasePath + "/" + $archetype; //print ("\ncreated " + $softPosesPathTmp);
				sysFile -makeDir $archetypesPathTmp;
				}

			poseLibPopulateUIList("casting");
			}

		// Clear the existing archetype elements before adding the new ones.
		string $allMenuItemsTmp[] = `optionMenu -q -ils plArchetypeChoiceOM`;
		for ($mi in $allMenuItemsTmp)
			{
			deleteUI $mi;
			}

		for ($archetype in $poseLibArchetypeList)
			{
			menuItem -label $archetype -p plArchetypeChoiceOM;
			}

		//print ("\nNumber of archetypes: " + `size $poseLibArchetypeList`);
		if ( `size $poseLibArchetypeList` == 1 )
			menuItem -e -en off plDeleteArchetypeMI;
		else
			menuItem -e -en on plDeleteArchetypeMI;

		return $poseLibArchetypeList;
		}

	else if ($data == "casting")
		{
		string $selectedArchetypeTmp = `optionMenu -q -v plArchetypeChoiceOM`;
		string $poseLibCastingList[] = poseLibFilterFolders(`getFileList -folder ($poseLibBasePath + "/" + $selectedArchetypeTmp + "/")`);

		// If there are no valid casting folders, then create directories for the default ones.
		if (`size $poseLibCastingList` == 0)
			{
			$poseLibCastingList = $poseLibDefaultCasting;

			for ($casting in $poseLibCastingList)
				{
				string $castingPathTmp = $poseLibBasePath + "/" + $selectedArchetypeTmp + "/" + $casting; //print ("\ncreated " + $softPosesPathTmp);
				sysFile -makeDir $castingPathTmp;
				}

			poseLibPopulateUIList("character");
			}

		// Clear the existing casting elements before adding the new ones.
		string $allMenuItemsTmp[] = `optionMenu -q -ils plCastingChoiceOM`;
		for ($mi in $allMenuItemsTmp)
			{
			deleteUI $mi;
			}

		for ($casting in $poseLibCastingList)
			{
			menuItem -label $casting -p plCastingChoiceOM;
			}

		if ( `size $poseLibCastingList` == 1 )
			menuItem -e -en off plDeleteCastingMI;
		else
			menuItem -e -en on plDeleteCastingMI;

		// Update the name over the chars/props/sets TSL.
		string $properNameTmp = `optionMenu -q -v plArchetypeChoiceOM`;
		text -e -l (`capitalizeString($properNameTmp)` + ":") plArchetypeText;

		return $poseLibCastingList;
		}

	else if ($data == "character")
		{
		string $selectedArchetypeTmp = `optionMenu -q -v plArchetypeChoiceOM`;
		string $selectedCastingTmp = `optionMenu -q -v plCastingChoiceOM`;
		//print ($poseLibBasePath + "/" + $selectedArchetypeTmp + "/" + $selectedCastingTmp + "/");
		string $poseLibCharacterList[] = poseLibFilterFolders(`getFileList -folder ($poseLibBasePath + $selectedArchetypeTmp + "/" + $selectedCastingTmp + "/")`);

		// If there are no valid characters folders, then create directories for the default ones.
		if (`size $poseLibCharacterList` == 0)
			{
			$poseLibCharacterList = $poseLibDefaultCharacters;

			for ($character in $poseLibCharacterList)
				{
				string $characterPathTmp = $poseLibBasePath + "/" + $selectedArchetypeTmp + "/" + $selectedCastingTmp + "/" + $character; //print ("\ncreated " + $softPosesPathTmp);
				sysFile -makeDir $characterPathTmp;
				}

			poseLibPopulateUIList("category");
			}

		// TexScrollLists return an array, so we have to assign index 0 to the variable.
		string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`;

		// Clear the existing character elements before adding the new ones.
		textScrollList -e -ra plCharacterChoiceTSL;
		for ($i=0;$i<`size $poseLibCharacterList`;$i++)
			{
			textScrollList -e -a $poseLibCharacterList[$i] plCharacterChoiceTSL;
			}

		if ( `size $poseLibCharacterList` == 1 )
			menuItem -e -en off plDeleteCharacterMI;
		else
			menuItem -e -en on plDeleteCharacterMI;

		// If a similar category exists for the newly selected character, then keep it selected.
		if (stringArrayContains($selectedCharacter[0], $poseLibCharacterList))
			textScrollList -e -si $selectedCharacter[0] plCharacterChoiceTSL;
		else if (`textScrollList -q -ni plCharacterChoiceTSL` > 0)
			textScrollList -e -si $poseLibCharacterList[0] plCharacterChoiceTSL;

		return $poseLibCharacterList;
		}

	else if ($data == "category")
		{
		string $selectedArchetypeTmp = `optionMenu -q -v plArchetypeChoiceOM`;
		//print ("\n$selectedArchetypeTmp = " + $selectedArchetypeTmp);
		string $selectedCastingTmp = `optionMenu -q -v plCastingChoiceOM`;

		// TexScrollLists return an array, so we have to assign index 0 to the variable.
		string $selectedCharacter[0] = `textScrollList -q -si plCharacterChoiceTSL`; //print ("\n$selectedCharacter[0] = " + $selectedCharacter[0]);
		//print ($poseLibBasePath + "/" + $selectedArchetypeTmp + "/" + $selectedCastingTmp + "/" + $selectedCharacter + "/");
		string $poseLibCategoryList[] = poseLibFilterFolders(`getFileList -folder ($poseLibBasePath + $selectedArchetypeTmp + "/" + $selectedCastingTmp + "/" + $selectedCharacter[0] + "/")`);

		// If there are no valid categories folders, then create directories for the default ones.
		if (`size $poseLibCategoryList` == 0)
			{
			$poseLibCategoryList = $poseLibDefaultCategories;

			for ($category in $poseLibCategoryList)
				{
				string $categoryPathTmp = $poseLibBasePath + "/" + $selectedArchetypeTmp + "/" + $selectedCastingTmp + "/" + $selectedCharacter[0] + "/" + $category; //print ("\ncreated " + $softPosesPathTmp);
				sysFile -makeDir $categoryPathTmp;
				}
			}

		// TexScrollLists return an array, so we have to assign index 0 to the variable.
		string $selectedCategory[0] = `textScrollList -q -si plCategoryChoiceTSL`;

		// Clear the existing category elements before adding the new ones.
		textScrollList -e -ra plCategoryChoiceTSL;
		for ($i=0;$i<`size $poseLibCategoryList`;$i++)
			{
			textScrollList -e -a $poseLibCategoryList[$i] -dpc "poseLibDropCallBack" plCategoryChoiceTSL;
			}

		if ( `size $poseLibCategoryList` == 1 )
			menuItem -e -en off plDeleteCategoryMI;
		else
			menuItem -e -en on plDeleteCategoryMI;

		// If a similar category exists for the newly selected character, then keep it selected.
		if (stringArrayContains($selectedCategory[0], $poseLibCategoryList))
			textScrollList -e -si $selectedCategory[0] plCategoryChoiceTSL;
		else if (`textScrollList -q -ni plCategoryChoiceTSL` > 0)
			textScrollList -e -si $poseLibCategoryList[0] plCategoryChoiceTSL;

		return $poseLibCategoryList;
		}
	}


global proc poseLib()
	{
	global string $poseLibVersion;
	global string $poseLibBasePath;
	global string $poseLibCurrentProject;
	global string $poseLibProjects[];
	global int $posesToTheRight;
	global int $poseLibIconsSize[];
	global int $poseLibCustomIconsSize[];

	// Delete any existing poseLib-related window.
	if (`window -exists poseLibWindow`)
		deleteUI poseLibWindow;

	if (`window -exists poseLibCreateNewPoseWindow`)
		deleteUI poseLibCreateNewPoseWindow;

	if (`window -exists renameArchetypeWindow`)
		deleteUI renameArchetypeWindow;

	if (`window -exists renameCharacterWindow`)
		deleteUI renameCharacterWindow;

	if (`window -exists renameCategoryWindow`)
		deleteUI renameCategoryWindow;

	if (`window -exists poseLibRenamePoseWindow`)
		deleteUI poseLibRenamePoseWindow;

	if (`window -exists poseLibCharCatManagerWindow`)
		deleteUI poseLibCharCatManagerWindow;

	if (`window -ex poseLibCreateNewProjectWindow`)
		deleteUI  poseLibCreateNewProjectWindow;

	if (`window -ex poseLibProjectOptionsWindow`)
		deleteUI  poseLibProjectOptionsWindow;

	if (`window -exists poseLibMovePoseWindow`)
		deleteUI poseLibMovePoseWindow;

	if (`window -ex renameCastingWindow`)
		deleteUI  renameCastingWindow;

	if (`window -ex poseLibArchetypeAndCastWindow`)
		deleteUI  poseLibArchetypeAndCastWindow;

	if (`window -ex poseLibTextEditorWindow`)
		deleteUI  poseLibTextEditorWindow;

	if (`window -exists poseLibCustomIconsSizeWindow`)
		deleteUI poseLibCustomIconsSizeWindow;

	if (`window -exists poseLibCaptureCameraOptionsWindow`)
		deleteUI poseLibCaptureCameraOptionsWindow;

	if (`window -exists createCategoryWindow`)
		deleteUI createCategoryWindow;

	if (`window -exists createCharacterWindow`)
		deleteUI createCharacterWindow;

	if (`window -exists createCastingWindow`)
		deleteUI createCastingWindow;

	if (`window -exists createArchetypeWindow`)
		deleteUI createArchetypeWindow;

	// ----------------
	// Build main UI.
	// ----------------
	window -tlb off -rtf off -sizeable true -menuBar true -mbv true -w 450 -title ("poseLib v" + $poseLibVersion + " - " + $poseLibCurrentProject + " - " + $poseLibBasePath) poseLibWindow;

		menu -label "Display" displayMenu;
			menuItem -label "Icons Size" -subMenu true iconsSizeMenu;
				radioMenuItemCollection -p iconsSizeMenu;
					menuItem -label "50 x 50" -radioButton off -c "global int $poseLibIconsSize[]; $poseLibIconsSize[0] = 50; $poseLibIconsSize[1] = 50; poseLibRefreshPoseList()" plIconsSize1MI;
					menuItem -label "64 x 64" -radioButton off -c "global int $poseLibIconsSize[]; $poseLibIconsSize[0] = 64; $poseLibIconsSize[1] = 64; poseLibRefreshPoseList()" plIconsSize2MI;
					menuItem -label "128 x 128" -radioButton off -c "global int $poseLibIconsSize[]; $poseLibIconsSize[0] = 128; $poseLibIconsSize[1] = 128; poseLibRefreshPoseList()" plIconsSize3MI;
					menuItem -divider true;
					menuItem -label "Custom (104 x 82)" -radioButton on -c "global int $poseLibIconsSize[]; global int $poseLibCustomIconsSize[]; $poseLibIconsSize[0] = $poseLibCustomIconsSize[0]; $poseLibIconsSize[1] = $poseLibCustomIconsSize[1]; poseLibRefreshPoseList()" plIconsSizeCustomMI;
						menuItem -ob true -c "poseLibCustomIconsSizeOptions()";
				setParent -menu displayMenu;

			menuItem -divider true;
			menuItem -label "Capture Camera" -c "poseLibCaptureCameraOptions()";
			menuItem -divider true;
			menuItem -label "Window Layout" -subMenu true windowLayoutMenu;
				radioMenuItemCollection -p windowLayoutMenu;
					menuItem -label "Icons to the right" -radioButton on -c "global int $posesToTheRight; $posesToTheRight = 1; poseLibSavePrefs(); poseLib()" plPosesToTheRightMI;
					menuItem -label "Icons to the left" -radioButton off -c "global int $posesToTheRight; $posesToTheRight = 0; poseLibSavePrefs(); poseLib()" plPosesToTheLeftMI;
				setParent -menu windowLayoutMenu;

		menu -label "Projects" poseLibProjectsMenu;
			radioMenuItemCollection poseLibProjectsRMIC;

		menu -label "Misc";
			menuItem -label "Set Text Editor" -c "poseLibChooseTextEditor()";

		menu -label "Help" -helpMenu true;
			menuItem -label "About Application...";

	int $h = 20;

	// --------------
	// Poses frame.
	// --------------
	string $form = `formLayout -numberOfDivisions 100 plMainFL`;
	string $posesFrameLayout = `frameLayout -m on -mw 0 -mh 0 -p plMainFL -l "Poses: " -fn "smallFixedWidthFont" -collapsable false -borderStyle "etchedIn" plPosesFL`;
	string $optionsFrameLayout = `frameLayout -lv off -p plMainFL -collapsable false -borderVisible off plOptionsFL`;
	shelfLayout -p plPosesFL -h 500 -st "iconAndTextVertical" plPosesSL;
	columnLayout -p plOptionsFL -adjustableColumn true plOptionsColumn;

	// ---------------
	// Namespace frame.
	// ---------------
	frameLayout -mw 0 -bv on -lv off -collapsable false -borderStyle "etchedIn" -w 190 -h 82 -p plOptionsColumn namespaceFrameLayoutFL;
		columnLayout -adjustableColumn true -p namespaceFrameLayoutFL;
			radioCollection namespaceChoiceRC;
				radioButton -label "Use Selection Namespace" -onc "textField -e -en off namespaceTextFieldTF" -ofc "textField -e -en on namespaceTextFieldTF" useSelectionNamespaceRB;
				radioButton -label "Use Pose Namespace" -onc "textField -e -en off namespaceTextFieldTF" -ofc "textField -e -en on namespaceTextFieldTF" usePoseNamespaceRB;
				radioButton -en on -label "Use Custom Namespace" useCustomNamespaceRB;
			textField -w 150 -en off -text "myNamespace" namespaceTextFieldTF;
			radioCollection -e -sl useSelectionNamespaceRB namespaceChoiceRC;
		setParent ..;
	setParent ..;

	separator -st "none" -h 4;

	frameLayout -mw 0 -bv on -lv off -collapsable false -borderStyle "etchedIn" -w 190 -p plOptionsColumn;
		columnLayout -adjustableColumn true;
			intSliderGrp -cw3 100 30 5 -label "ALT/CTRL Pose %: " -field true -minValue 5 -maxValue 95
				-fieldMinValue 5 -fieldMaxValue 95
				-value 25 -ann "Amount of pose applied when pressing ALT key" plPoseFractionISG;
		setParent ..;
	setParent ..;

	separator -st "none" -h 4;

	// --------------------
	// Library choice frame
	// --------------------
	frameLayout -mw 0 -bv off -lv off -collapsable false -p plOptionsColumn framePath;
		columnLayout -adjustableColumn true -p framePath pathPosesColumn;
			rowColumnLayout -nc 2 -cw 1 158 -cw 2 1;
				optionMenu -h ($h) -w 80 -label "    Library Status:  " -cc "poseLibSwitchLibrary()" libraryStatusChoiceOM;
					menuItem -label "Public" -p libraryStatusChoiceOM;
					menuItem -label "Private" -p libraryStatusChoiceOM;
				setParent -menu..;
				separator -style "none";
			setParent ..;
		setParent ..;
	setParent ..;

	separator -st none -h 4 -w 150;

	frameLayout -mw 0 -bv on -lv off -collapsable false -w 230 -p plOptionsColumn archetypeCastingFL;
		columnLayout -adjustableColumn true archetypeCastingCL;
			rowColumnLayout -nc 5 -cw 1 36 -cw 2 76 -cw 3 30 -cw 4 70 -cw 5 2 characterCategoryRCL;
				text -l "Type:";
				optionMenu -h ($h) -cc "poseLibPopulateUIList(\"casting\"); poseLibPopulateUIList(\"character\"); poseLibPopulateUIList(\"category\"); poseLibRefreshPoseList()" plArchetypeChoiceOM;
					popupMenu -p plArchetypeChoiceOM -button 3 plArchetypePM;
						menuItem -en on -p plArchetypePM -label "Rename Selected Type" -c "poseLibRenameArchetype()";
						menuItem -divider true;
						menuItem -en on -p plArchetypePM -label "Delete Selected Type" -c "poseLibDeleteArchetype()" plDeleteArchetypeMI;
						menuItem -divider true;
						menuItem -en on -p plArchetypePM -label "Create New Type" -c "poseLibCreateNewArchetype()";

				text -l "Cast:";
				optionMenu -h ($h) -cc "poseLibPopulateUIList(\"character\"); poseLibPopulateUIList(\"category\"); poseLibRefreshPoseList()" plCastingChoiceOM;
					popupMenu -p plCastingChoiceOM -button 3 plCastingPM;
						menuItem -en on -p plCastingPM -label "Rename Selected Cast" -c "poseLibRenameCasting()";
						menuItem -divider true;
						menuItem -en on -p plCastingPM -label "Delete Selected Cast" -c "poseLibDeleteCasting()" plDeleteCastingMI;
						menuItem -divider true;
						menuItem -en on -p plCastingPM -label "Create New Cast" -c "poseLibCreateNewCasting()";

				separator -st "none" -h 4;
			setParent ..;

			separator -st "in" -h 4;

			rowColumnLayout -nc 2 -cw 1 112 -cw 2 112 plCharactersCategoriesRCL;
				text -l "Characters:" plArchetypeText;
				text -l "Categories:";
				textScrollList -nr 8 -height 200 -ams 0 -sc ("poseLibPopulateUIList(\"category\"); poseLibRefreshPoseList()") plCharacterChoiceTSL;
					popupMenu -p plCharacterChoiceTSL -button 3 charPopupMenu;
						menuItem -p charPopupMenu -en on -label "Rename Selected Character" -c "poseLibRenameCharacter()";
						menuItem -p charPopupMenu -divider true;
						menuItem -p charPopupMenu -en on -label "Delete Selected Character" -c "poseLibDeleteCharacter()" plDeleteCharacterMI;
						menuItem -p charPopupMenu -divider true;
						menuItem -p charPopupMenu -en on -label "Create New Character" -c "poseLibCreateNewCharacter()";

				textScrollList -nr 8 -height 200 -ams 0 -sc ("poseLibRefreshPoseList()") plCategoryChoiceTSL;
					popupMenu -p plCategoryChoiceTSL -button 3 catPopupMenu;
						menuItem -p catPopupMenu -en on -label "Rename Selected Character" -c "poseLibRenameCategory()";
						menuItem -p catPopupMenu -divider true;
						menuItem -p catPopupMenu -en on -label "Delete Selected Category" -c "poseLibDeleteCategory()" plDeleteCategoryMI;
						menuItem -p catPopupMenu -divider true;
						menuItem -p catPopupMenu -en on -label "Create New Category" -c "poseLibCreateNewCategory()";

				button -h ($h-4) -l "<<" -c "poseLibChangeOptionsColumnWidth(\"narrower\")";
				button -h ($h-4) -l ">>" -c "poseLibChangeOptionsColumnWidth(\"wider\")";
			setParent ..;
		setParent ..;
	setParent ..;

	// --------------------
	// Create pose frame
	// --------------------
	frameLayout -mw 0 -bv off -lv off -collapsable false  -p plOptionsColumn frameCreate;
		columnLayout -adjustableColumn true -p frameCreate setPosesColumn;

			separator -st none -h 4;
			button -label "Create New Pose!" -h 48 -w 156
				-ann "Create a new pose based on the current selection" -c ("poseLibCreateNewPose(\"\")") newPoseButton;
		setParent ..;
	setParent ..;

	separator -st none -h 4;

	button -label "Save Preferences" -h ($h+4) -ann "Save poseLib preferences" -c "poseLibSavePrefs()" savePrefsButton;

	separator -st "none" -h 2;

	text -al "right" -bgc .3 .4 .3 -fn "smallBoldLabelFont" -l "SeithCG.com ";
	//text -al "left" /*-bgc .3 .4 .3*/ -fn "smallBoldLabelFont" -l "Registered to: Toto";

	// ---------------------------------------------------------------------

	// Finally show the window.
	showWindow poseLibWindow;

	// Load the preferences.
	poseLibLoadPrefs();

	// Now populate the projects menu.
	for ($i=0;$i<`size $poseLibProjects`;$i++)
		{
		menuItem -label $poseLibProjects[$i] -c ("poseLibSwitchProject(\"" + $poseLibProjects[$i] + "\"); poseLib()") -radioButton off -p poseLibProjectsMenu ($poseLibProjects[$i] + "MI");
		menuItem -ob true -p poseLibProjectsMenu -c ("poseLibProjectOptions(\"" + $poseLibProjects[$i] + "\")");
		}

	menuItem -p poseLibProjectsMenu -divider true;
	menuItem -p poseLibProjectsMenu -label "Create New Project" -c "poseLibCreateNewProject()";
	
	print ("\n=======> $poseLibCurrentProject at start = " + $poseLibCurrentProject);
	// Update the current projects in the main UI.
	int $projectWasNotFound = catchQuiet(`menuItem -e -radioButton true ($poseLibCurrentProject + "MI")`);
	if ($projectWasNotFound == 1)
		{
		$poseLibCurrentProject = $poseLibProjects[0];
		menuItem -e -radioButton true ($poseLibCurrentProject + "MI");
		}

	// Select the correct icons size option in the Display menu.
	if ( $poseLibIconsSize[0] == 50 && $poseLibIconsSize[1] == 50 )
		menuItem -e -radioButton true "plIconsSize1MI";
	else if ( $poseLibIconsSize[0] == 64 && $poseLibIconsSize[1] == 64 )
		menuItem -e -radioButton true "plIconsSize2MI";
	else if ( $poseLibIconsSize[0] == 128 && $poseLibIconsSize[1] == 128 )
		menuItem -e -radioButton true "plIconsSize3MI";
	else
		menuItem -e -radioButton true -label ("Custom (" + $poseLibCustomIconsSize[0] + " x " + $poseLibCustomIconsSize[1] + ")") plIconsSizeCustomMI;

	// Make sure the project appears in the main window's title.
	window -e -title ("poseLib v" + $poseLibVersion + " - " + $poseLibCurrentProject + " - " + $poseLibBasePath) poseLibWindow;

	// --------------------
	// Global Layout
	// --------------------
	// Poses frame to the right, options to the left.
	if ( $posesToTheRight )
		{
		formLayout -edit
			-attachForm     $optionsFrameLayout "top"    0
			-attachNone     $optionsFrameLayout "right"
			-attachForm     $optionsFrameLayout "bottom" 2
			-attachForm     $optionsFrameLayout "left"  2

			-attachForm     $posesFrameLayout "top"  2
			-attachForm     $posesFrameLayout "right"     2
			-attachForm     $posesFrameLayout "bottom" 2
			-attachControl  $posesFrameLayout "left"    2 $optionsFrameLayout
		$form;

		menuItem -e -radioButton on plPosesToTheRightMI;
		}
	// Poses frame to the left, options to the right.
	else
		{
		formLayout -edit
			-attachForm     $posesFrameLayout "top"    0
			-attachControl  $posesFrameLayout "right" 2 $optionsFrameLayout
			-attachForm     $posesFrameLayout "bottom" 2
			-attachForm     $posesFrameLayout "left"  2

			-attachForm     $optionsFrameLayout "top"      2
			-attachForm     $optionsFrameLayout "right" 2
			-attachForm     $optionsFrameLayout "bottom" 2
			-attachNone     $optionsFrameLayout "left"
		$form;

		menuItem -e -radioButton on plPosesToTheLeftMI;
		}

	// Refresh the poses icons.
	evalDeferred("poseLibRefreshPoseList()");
	}


poseLib;